import MathematicalLogic.SecondOrder.Semantics
import Mathlib.Algebra.Field.Basic
import Mathlib.Data.Real.Sqrt

namespace SecondOrder.Language

inductive OrderedField.Func : â„• â†’ Type where
| zero : Func 0
| one : Func 0
| add : Func 2
| neg : Func 1
| mul : Func 2
| inv : Func 1

inductive OrderedField.Rel : â„• â†’ Type where
| le : Rel 2

def OrderedField : Language where
  Func := OrderedField.Func
  Rel := OrderedField.Rel

instance : Zero (OrderedField.Term Î“) := âŸ¨.zero â¬á¶  []áµ¥âŸ©
instance : One (OrderedField.Term Î“) := âŸ¨.one â¬á¶  []áµ¥âŸ©
instance : Add (OrderedField.Term Î“) := âŸ¨(.add â¬á¶  [Â·, Â·]áµ¥)âŸ©
instance : Neg (OrderedField.Term Î“) := âŸ¨(.neg â¬á¶  [Â·]áµ¥)âŸ©
instance : Sub (OrderedField.Term Î“) := âŸ¨(Â· + -Â·)âŸ©
instance : Mul (OrderedField.Term Î“) := âŸ¨(.mul â¬á¶  [Â·, Â·]áµ¥)âŸ©
instance : Inv (OrderedField.Term Î“) := âŸ¨(.inv â¬á¶  [Â·]áµ¥)âŸ©
instance : Div (OrderedField.Term Î“) := âŸ¨(Â· * Â·â»Â¹)âŸ©

def Formula.le (tâ‚ tâ‚‚ : OrderedField.Term Î“) : OrderedField.Formula Î“ := .le â¬Ê³ [tâ‚, tâ‚‚]áµ¥
infix:60 " âª " => Formula.le

namespace Theory

inductive Real : OrderedField.Theory where
| add_assoc : Real (âˆ€' (âˆ€' (âˆ€' (#2 + #1 + #0 â‰ #2 + (#1 + #0)))))
| add_comm : Real (âˆ€' (âˆ€' (#1 + #0 â‰ #0 + #1)))
| add_zero : Real (âˆ€' (#0 + 0 â‰ #0))
| add_left_neg : Real (âˆ€' (-#0 + #0 â‰ 0))
| mul_assoc : Real (âˆ€' (âˆ€' (âˆ€' (#2 * #1 * #0 â‰ #2 * (#1 * #0)))))
| mul_comm : Real (âˆ€' (âˆ€' (#1 * #0 â‰ #0 * #1)))
| mul_one : Real (âˆ€' (#0 * 1 â‰ #0))
| inv_zero : Real (0â»Â¹ â‰ 0)
| mul_inv_cancel : Real (âˆ€' (~ #0 â‰ 0 â‡’ #0 * (#0)â»Â¹ â‰ 1))
| left_distrib : Real (âˆ€' (âˆ€' (âˆ€' (#2 * (#1 + #0) â‰ #2 * #1 + #2 * #0))))
| zero_ne_one : Real (~ 0 â‰ 1)
| le_refl : Real (âˆ€' (#0 âª #0))
| le_antisymm : Real (âˆ€' (âˆ€' (#1 âª #0 â‡’ #0 âª #1 â‡’ #1 â‰ #0)))
| le_trans : Real (âˆ€' (âˆ€' (âˆ€' (#2 âª #1 â‡’ #1 âª #0 â‡’ #2 âª #0))))
| le_total : Real (âˆ€' (âˆ€' (#1 âª #0 â©’ #0 âª #1)))
| add_le_add : Real (âˆ€' (âˆ€' (âˆ€' (#2 âª #1 â‡’ #2 + #0 âª #1 + #0))))
| mul_le_mul : Real (âˆ€' (âˆ€' (âˆ€' (#2 âª #1 â‡’ 0 âª #0 â‡’ #2 * #0 âª #1 * #0))))
| exists_lub : Real (âˆ€Ê³ 1 (âˆƒ' (1 â¬Ê³áµ› [#0]áµ¥) â‡’ âˆƒ' (âˆ€' (2 â¬Ê³áµ› [#0]áµ¥ â‡’ #0 âª #1)) â‡’ âˆƒ' (âˆ€' (2 â¬Ê³áµ› [#0]áµ¥ â‡’ #0 âª #1) â©‘ âˆ€' (âˆ€' (3 â¬Ê³áµ› [#0]áµ¥ â‡’ #0 âª #1) â‡’ #1 âª #0))))

attribute [local simp] Structure.satisfy Structure.interpFormula Structure.interpTerm Structure.Assignment.cons
  Vec.eq_two Vec.eq_one Vec.eq_nil

noncomputable def Real.ğ“¡ : Real.Model where
  Dom := â„
  interpFunc
  | .zero, _ => 0
  | .one, _ => 1
  | .add, v => v 0 + v 1
  | .neg, v => - v 0
  | .mul, v => v 0 * v 1
  | .inv, v => (v 0)â»Â¹
  interpRel
  | .le, v => v 0 â‰¤ v 1
  satisfy_theory p h := by
    cases h with simp
    | add_assoc => apply _root_.add_assoc
    | add_comm => apply _root_.add_comm
    | mul_assoc => apply _root_.mul_assoc
    | mul_comm => apply _root_.mul_comm
    | mul_inv_cancel => apply _root_.mul_inv_cancel
    | left_distrib => apply _root_.left_distrib
    | le_antisymm => apply _root_.le_antisymm
    | le_trans => apply _root_.le_trans
    | le_total => intro a b; apply le_of_lt
    | mul_le_mul => intro a b c; apply mul_le_mul_of_nonneg_right
    | exists_lub =>
      intro R a hâ‚ b hâ‚‚
      exists sSup (R [Â·]áµ¥)
      exact Real.isLUB_sSup (R [Â·]áµ¥) âŸ¨a, hâ‚âŸ© âŸ¨b, hâ‚‚âŸ©

namespace Model

variable {ğ“œ : Real.Model} (a b c : ğ“œ)

namespace Real
instance : Zero ğ“œ := âŸ¨ğ“œ.interpFunc .zero []áµ¥âŸ©
instance : One ğ“œ := âŸ¨ğ“œ.interpFunc .one []áµ¥âŸ©
instance : Add ğ“œ := âŸ¨(ğ“œ.interpFunc .add [Â·, Â·]áµ¥)âŸ©
instance : Neg ğ“œ := âŸ¨(ğ“œ.interpFunc .neg [Â·]áµ¥)âŸ©
instance : Mul ğ“œ := âŸ¨(ğ“œ.interpFunc .mul [Â·, Â·]áµ¥)âŸ©
instance : Inv ğ“œ := âŸ¨(ğ“œ.interpFunc .inv [Â·]áµ¥)âŸ©
end Real

theorem add_assoc : a + b + c = a + (b + c) := by
  have := ğ“œ.satisfy_theory _ .add_assoc a b c
  simp at this; exact this

theorem add_comm : a + b = b + a := by
  have := ğ“œ.satisfy_theory _ .add_comm a b
  simp at this; exact this

theorem add_zero : a + 0 = a := by
  have := ğ“œ.satisfy_theory _ .add_zero a
  simp at this; exact this

theorem zero_add : 0 + a = a := by rw [add_comm, add_zero]

theorem add_left_neg : -a + a = 0 := by
  have := ğ“œ.satisfy_theory _ .add_left_neg a
  simp at this; exact this

instance : AddCommGroup ğ“œ where
  add_assoc := add_assoc
  add_comm := add_comm
  zero_add := zero_add
  add_zero := add_zero
  add_left_neg := add_left_neg
  nsmul := nsmulRec
  zsmul := zsmulRec

theorem mul_assoc : a * b * c = a * (b * c) := by
  have := ğ“œ.satisfy_theory _ .mul_assoc a b c
  simp at this; exact this

theorem mul_comm : a * b = b * a := by
  have := ğ“œ.satisfy_theory _ .mul_comm a b
  simp at this; exact this

theorem mul_one : a * 1 = a := by
  have := ğ“œ.satisfy_theory _ .mul_one a
  simp at this; exact this

theorem one_mul : 1 * a = a := by rw [mul_comm, mul_one]

theorem inv_zero : (0 : ğ“œ)â»Â¹ = 0 := by
  have := ğ“œ.satisfy_theory _ .inv_zero
  simp at this; exact this

theorem mul_inv_cancel (h : a â‰  0) : a * aâ»Â¹ = 1 := by
  have := ğ“œ.satisfy_theory _ .mul_inv_cancel a
  simp at this; exact this h

theorem left_distrib : a * (b + c) = a * b + a * c := by
  have := ğ“œ.satisfy_theory _ .left_distrib a b c
  simp at this; exact this

theorem right_distrib : (a + b) * c = a * c + b * c := by rw [mul_comm, left_distrib, mul_comm c a, mul_comm c b]

theorem zero_mul : 0 * a = 0 := by
  apply add_left_cancel (a := 0 * a)
  rw [â†right_distrib, add_zero, add_zero]

theorem mul_zero : a * 0 = 0 := by rw [mul_comm, zero_mul]

theorem zero_ne_one : (0 : ğ“œ) â‰  1 := by
  have := ğ“œ.satisfy_theory _ .zero_ne_one
  simp at this; exact this

instance : Field ğ“œ where
  add_left_neg := add_left_neg
  mul_assoc := mul_assoc
  mul_comm := mul_comm
  mul_one := mul_one
  one_mul := one_mul
  inv_zero := inv_zero
  mul_inv_cancel := mul_inv_cancel
  left_distrib := left_distrib
  right_distrib := right_distrib
  zero_mul := zero_mul
  mul_zero := mul_zero
  exists_pair_ne := âŸ¨0, 1, zero_ne_oneâŸ©
  zsmul := zsmulRec
  qsmul := _
  nnqsmul := _

instance : LE ğ“œ := âŸ¨(ğ“œ.interpRel .le [Â·, Â·]áµ¥)âŸ©

theorem le_refl : a â‰¤ a := by
  have := ğ“œ.satisfy_theory _ .le_refl a
  simp at this; exact this

theorem le_antisymm : a â‰¤ b â†’ b â‰¤ a â†’ a = b := by
  have := ğ“œ.satisfy_theory _ .le_antisymm a b
  simp at this; exact this

theorem le_trans : a â‰¤ b â†’ b â‰¤ c â†’ a â‰¤ c := by
  have := ğ“œ.satisfy_theory _ .le_trans a b c
  simp at this; exact this

theorem le_total : a â‰¤ b âˆ¨ b â‰¤ a := by
  have := ğ“œ.satisfy_theory _ .le_total a b
  simp at this; rw [or_iff_not_imp_left]; exact this

open Classical

noncomputable instance : LinearOrder ğ“œ where
  le_refl := le_refl
  le_antisymm := le_antisymm
  le_trans := le_trans
  le_total := le_total
  decidableLE := _

theorem add_le_add_right : a â‰¤ b â†’ a + c â‰¤ b + c := by
  have := ğ“œ.satisfy_theory _ .add_le_add a b c
  simp at this; exact this

theorem add_le_add_left : a â‰¤ b â†’ c + a â‰¤ c + b := by
  rw [add_comm c a, add_comm c b]
  apply add_le_add_right

theorem zero_le_neg_iff : 0 â‰¤ -a â†” a â‰¤ 0 := by
  constructor
  Â· intro h
    apply add_le_add_left _ _ a at h
    rw [add_zero, add_right_neg] at h
    exact h
  Â· intro h
    apply add_le_add_left _ _ (-a) at h
    rw [add_zero, add_left_neg] at h
    exact h

theorem mul_le_mul_right : a â‰¤ b â†’ 0 â‰¤ c â†’ a * c â‰¤ b * c := by
  have := ğ“œ.satisfy_theory _ .mul_le_mul a b c
  simp at this; exact this

theorem zero_le_one : (0 : ğ“œ) â‰¤ 1 := by
  by_contra hâ‚
  simp at hâ‚
  have hâ‚‚ : (0 : ğ“œ) â‰¤ -1 := by
    rw [zero_le_neg_iff 1]
    exact le_of_lt hâ‚
  have hâ‚ƒ := lt_of_lt_of_le hâ‚ hâ‚‚
  have hâ‚„ := mul_le_mul_right _ _ _ (le_of_lt hâ‚ƒ) hâ‚‚
  rw [mul_neg_one, mul_neg_one, neg_neg] at hâ‚„
  have hâ‚… := le_antisymm _ _ hâ‚„ (le_of_lt hâ‚ƒ)
  simp [hâ‚…] at hâ‚ƒ

theorem mul_pos : 0 < a â†’ 0 < b â†’ 0 < a * b := by
  intro hâ‚ hâ‚‚
  apply lt_of_le_of_ne
  Â· have hâ‚ƒ := mul_le_mul_right _ _ _ (le_of_lt hâ‚) (le_of_lt hâ‚‚)
    rw [zero_mul] at hâ‚ƒ; exact hâ‚ƒ
  Â· simp [ne_of_gt hâ‚, ne_of_gt hâ‚‚]

noncomputable instance : LinearOrderedField ğ“œ where
  mul_comm := mul_comm
  inv_zero := inv_zero
  mul_inv_cancel := mul_inv_cancel
  qsmul := _
  nnqsmul := _
  decidableLE := _
  le_total := le_total
  add_le_add_left a b h c := add_le_add_left a b c h
  zero_le_one := zero_le_one
  mul_pos := mul_pos

theorem exists_lub (s : Set ğ“œ) : s.Nonempty â†’ BddAbove s â†’ âˆƒ u, IsLUB s u := by
  intro âŸ¨x, hâ‚âŸ© âŸ¨y, hâ‚‚âŸ©
  have := ğ“œ.satisfy_theory _ .exists_lub
  simp at this
  exact this (Â·.head âˆˆ s) x hâ‚ y hâ‚‚

noncomputable def ofReal (x : â„) : ğ“œ :=
  Classical.choose
    (exists_lub { â†‘y | (y : â„š) (_ : â†‘y â‰¤ x) }
      ((exists_rat_lt x).elim Î» y h => âŸ¨y, y, le_of_lt h, rflâŸ©)
      ((exists_rat_gt x).elim Î» y h => âŸ¨y, by simp [upperBounds]; intro z; rw [â†@Rat.cast_le z y â„]; exact le_trans' (le_of_lt h)âŸ©))

variable {x y z : â„} {q : â„š}

theorem ofReal_isLUB : IsLUB { â†‘y | (y : â„š) (_ : â†‘y â‰¤ x) } (ğ“œ.ofReal x) := Classical.choose_spec _

theorem ofReal_rat : ğ“œ.ofReal q = q := by
  apply ofReal_isLUB.unique
  constructor
  Â· intro y; simp; intro z hâ‚ hâ‚‚; simp [â†hâ‚‚, hâ‚]
  Â· intro y hâ‚; simp [upperBounds] at hâ‚; exact hâ‚ q (by rfl)

theorem ofReal_zero : ğ“œ.ofReal 0 = 0 := by
  have := @ofReal_rat ğ“œ 0
  simp at this; exact this

theorem ofReal_one : ğ“œ.ofReal 1 = 1 := by
  have := @ofReal_rat ğ“œ 1
  simp at this; exact this

theorem ofReal_lt : ğ“œ.ofReal x < ğ“œ.ofReal y â†” x < y := by
  constructor
  Â· intro hâ‚
    simp [isLUB_lt_iff ofReal_isLUB, upperBounds] at hâ‚
    rcases hâ‚ with âŸ¨a, hâ‚, hâ‚‚âŸ©
    by_contra hâ‚ƒ; simp at hâ‚ƒ
    apply not_lt_of_le _ hâ‚‚
    simp [isLUB_le_iff ofReal_isLUB, upperBounds]
    intro s hâ‚„
    apply hâ‚
    exact hâ‚„.trans hâ‚ƒ
  Â· intro hâ‚
    simp [isLUB_lt_iff ofReal_isLUB, upperBounds]
    rcases exists_rat_btwn hâ‚ with âŸ¨q, hâ‚‚, hâ‚ƒâŸ©
    exists q
    constructor
    Â· intro r hâ‚„
      have := hâ‚„.trans (le_of_lt hâ‚‚)
      simp at this; simp [this]
    Â· simp [lt_isLUB_iff ofReal_isLUB]
      rcases exists_rat_btwn hâ‚ƒ with âŸ¨r, hâ‚ƒ, hâ‚„âŸ©
      simp at hâ‚ƒ
      exists r, le_of_lt hâ‚„

theorem ofReal_le : ğ“œ.ofReal x â‰¤ ğ“œ.ofReal y â†” x â‰¤ y := by
  rw [â†not_iff_not, not_le, not_le, ofReal_lt]

theorem ofReal_injective : Function.Injective ğ“œ.ofReal := by
  intro x y h
  apply _root_.le_antisymm <;> simp [â†ğ“œ.ofReal_le, h]

lemma exists_nat_gt (a : ğ“œ) : âˆƒ (n : â„•), a < n := by
  by_contra hâ‚
  push_neg at hâ‚
  let s : Set ğ“œ := { â†‘n | (n : â„•) }
  have hâ‚‚ : s.Nonempty := by exists 0; simp [s]
  have hâ‚ƒ : BddAbove s := by exists a; simp [s, upperBounds]; exact hâ‚
  rcases exists_lub s hâ‚‚ hâ‚ƒ with âŸ¨b, hâ‚„âŸ©
  have hâ‚„ : b â‰¤ b - 1 := by
    apply hâ‚„.right
    simp [upperBounds, s]
    intro n
    rw [le_sub_iff_add_le, â†Nat.cast_add_one]
    exact hâ‚„.left âŸ¨n + 1, rflâŸ©
  simp at hâ‚„
  exact not_lt_of_ge hâ‚„ zero_lt_one

instance : Archimedean ğ“œ where
  arch x y h := by
    rcases exists_nat_gt (x / y) with âŸ¨n, h'âŸ©
    simp [div_lt_iff h] at h'
    exists n
    simp [le_of_lt h']

theorem ofReal_surjective : Function.Surjective ğ“œ.ofReal := by
  intro a
  let s : Set â„ := { â†‘q | (q : â„š) (_ : q â‰¤ a) }
  have hâ‚ : s.Nonempty := by
    rcases exists_rat_lt a with âŸ¨q, hâ‚âŸ©
    exists q; simp [s]; exact le_of_lt hâ‚
  have hâ‚‚ : BddAbove s := by
    rcases exists_rat_gt a with âŸ¨q, hâ‚âŸ©
    exists q; simp [upperBounds, s]
    intro r hâ‚‚
    have := hâ‚‚.trans (le_of_lt hâ‚)
    simp at this; exact this
  exists sSup s
  apply ofReal_isLUB.unique
  constructor
  Â· simp [upperBounds]
    intro q hâ‚ƒ
    apply le_of_forall_rat_lt_imp_le
    intro r hâ‚„; simp at hâ‚„
    have hâ‚… := lt_of_lt_of_le (Rat.cast_lt.mpr hâ‚„) hâ‚ƒ
    rw [lt_isLUB_iff (Real.isLUB_sSup _ hâ‚ hâ‚‚)] at hâ‚…
    rcases hâ‚… with âŸ¨_, âŸ¨r', hâ‚…, hâ‚†âŸ©, hâ‚‡âŸ©
    subst hâ‚†; simp at hâ‚‡
    apply le_trans' hâ‚…
    apply le_of_lt; simp [hâ‚‡]
  Â· intro b hâ‚ƒ
    simp [upperBounds] at hâ‚ƒ
    apply le_of_forall_rat_lt_imp_le
    intro r hâ‚„
    apply hâ‚ƒ
    apply le_csSup hâ‚‚
    exists r
    simp [le_of_lt hâ‚„]

theorem ofReal_add : ğ“œ.ofReal (x + y) = ğ“œ.ofReal x + ğ“œ.ofReal y := by
  apply ofReal_isLUB.unique
  constructor
  Â· intro z; simp; intro q hâ‚ hâ‚‚; subst hâ‚‚
    apply le_of_forall_rat_lt_imp_le
    intro r hâ‚‚; simp at hâ‚‚
    let Î´ := (x + y - r) / 2
    have hâ‚ƒ : 0 < Î´ := by simp [Î´]; exact lt_of_lt_of_le (Rat.cast_lt.mpr hâ‚‚) hâ‚
    rcases exists_rat_btwn (sub_lt_self x hâ‚ƒ) with âŸ¨râ‚, hâ‚„, hâ‚…âŸ©
    rcases exists_rat_btwn (sub_lt_self y hâ‚ƒ) with âŸ¨râ‚‚, hâ‚†, hâ‚‡âŸ©
    trans râ‚ + râ‚‚
    Â· have := add_lt_add hâ‚„ hâ‚†
      simp [sub_add_sub_comm, Î´, â†Rat.cast_add] at this
      rw [â†Rat.cast_add, Rat.cast_le]
      exact le_of_lt this
    Â· apply add_le_add <;> rw [â†ofReal_rat, ofReal_le] <;> apply le_of_lt <;> assumption
  Â· intro a hâ‚; simp [upperBounds] at hâ‚
    apply le_of_forall_rat_lt_imp_le
    intro q hâ‚‚
    let Î´ := (ğ“œ.ofReal x + ğ“œ.ofReal y - q) / 2
    have hâ‚ƒ : 0 < Î´ := by simp [Î´, hâ‚‚]
    rcases exists_rat_btwn (sub_lt_self (ğ“œ.ofReal x) hâ‚ƒ) with âŸ¨râ‚, hâ‚„, hâ‚…âŸ©
    rcases exists_rat_btwn (sub_lt_self (ğ“œ.ofReal y) hâ‚ƒ) with âŸ¨râ‚‚, hâ‚†, hâ‚‡âŸ©
    trans râ‚ + râ‚‚
    Â· have := add_lt_add hâ‚„ hâ‚†
      simp [sub_add_sub_comm, Î´, â†Rat.cast_add] at this
      rw [â†Rat.cast_add, Rat.cast_le]
      exact le_of_lt this
    Â· rw [â†Rat.cast_add]; apply hâ‚; simp
      apply add_le_add <;> rw [â†ğ“œ.ofReal_le, ofReal_rat] <;> apply le_of_lt <;> assumption

theorem ofReal_neg : ğ“œ.ofReal (-x) = -ğ“œ.ofReal x := by
  rw [eq_neg_iff_add_eq_zero, â†ofReal_add, _root_.add_left_neg, ofReal_zero]

lemma exists_sqrt (h : 0 â‰¤ a) : âˆƒ b, 0 â‰¤ b âˆ§ b ^ 2 = a := by
  let s : Set ğ“œ := { x | 0 â‰¤ x âˆ§ x ^ 2 â‰¤ a }
  have hâ‚ : s.Nonempty := by exists 0; simp [s, h]
  have hâ‚‚ : BddAbove s := by
    rcases exists_nat_gt a with âŸ¨n, hâ‚‚âŸ©
    have hâ‚ƒ : 0 < n := by
      have := lt_of_le_of_lt h hâ‚‚
      simp at this; exact this
    exists n
    intro b âŸ¨hâ‚„, hâ‚…âŸ©
    rw [â†pow_le_pow_iff_left (n := 2)]
    Â· apply hâ‚….trans; apply (le_of_lt hâ‚‚).trans
      apply le_self_pow
      Â· rw [â†ofReal_one, â†Rat.cast_natCast, â†ofReal_rat, ofReal_le]
        simp [Nat.succ_le]; exact Nat.pos_of_ne_zero (ne_zero_of_lt hâ‚ƒ)
      Â· simp
    Â· exact hâ‚„
    Â· simp
    Â· simp
  rcases exists_lub s hâ‚ hâ‚‚ with âŸ¨b, hâ‚ƒâŸ©
  exists b
  have hâ‚„ : 0 â‰¤ b := by apply hâ‚ƒ.left; simp [s]; exact h
  simp [hâ‚„]
  by_contra hâ‚…
  apply lt_or_gt_of_ne at hâ‚…
  rcases hâ‚… with hâ‚… | hâ‚…
  Â· let Î´ := min 1 ((a - b ^ 2) / (2 * b + 1))
    have hâ‚† : 0 < Î´ := by
      apply lt_min (by simp)
      apply div_pos
      Â· simp; exact hâ‚…
      Â· apply lt_of_lt_of_le zero_lt_one; simp; exact hâ‚„
    have hâ‚‡ : (b + Î´) ^ 2 â‰¤ a := by
      calc
        (b + Î´) ^ 2 = b ^ 2 + Î´ * (2 * b + Î´) := by rw [add_pow_two, mul_add, â†add_assoc, mul_comm Î´, pow_two Î´]
        _ â‰¤ b ^ 2 + Î´ * (2 * b + 1) := by simp [add_le_add_iff_left, mul_le_mul_left hâ‚†]; apply min_le_left
        _ â‰¤ b ^ 2 + (a - b ^ 2) := by
          rw [add_le_add_iff_left, â†le_div_iff]
          Â· apply min_le_right
          Â· apply lt_of_lt_of_le zero_lt_one; simp; exact hâ‚„
        _ â‰¤ a := by simp
    have hâ‚ˆ : b + Î´ âˆˆ s := âŸ¨add_nonneg hâ‚„ (le_of_lt hâ‚†), hâ‚‡âŸ©
    apply hâ‚ƒ.left at hâ‚ˆ
    simp at hâ‚ˆ
    exact not_lt_of_ge hâ‚ˆ hâ‚†
  Â· have hâ‚† : 0 < b := by
      by_contra hâ‚†; simp at hâ‚†; rw [_root_.le_antisymm hâ‚† hâ‚„] at hâ‚…; simp at hâ‚…; exact not_lt_of_ge h hâ‚…
    let Î´ := (b ^ 2 - a) / (2 * b)
    have hâ‚‡ : 0 < Î´ := by
      apply div_pos
      Â· simp; exact hâ‚…
      Â· simp; exact hâ‚†
    have hâ‚ˆ : (b - Î´) ^ 2 â‰¥ a := by
      calc
        (b - Î´) ^ 2 = b ^ 2 - Î´ * (2 * b - Î´) := by rw [sub_pow_two, mul_sub, â†sub_add, mul_comm Î´, pow_two Î´]
        _ â‰¥ b ^ 2 - Î´ * (2 * b) := by apply sub_le_sub_left; rw [mul_le_mul_left hâ‚‡]; simp; exact le_of_lt hâ‚‡
        _ â‰¥ b ^ 2 - (b ^ 2 - a) := by apply sub_le_sub_left; rw [â†le_div_iff]; simp; exact hâ‚†
        _ â‰¥ a := by simp
    have hâ‚‰ : b - Î´ âˆˆ upperBounds s := by
      intro c âŸ¨h', h''âŸ©
      apply le_trans' hâ‚ˆ at h''
      rw [pow_le_pow_iff_left] at h''
      Â· exact h''
      Â· exact h'
      Â· simp [Î´]; rw [div_le_iff]
        Â· simp [two_mul, mul_add, pow_two, add_assoc]
          exact add_nonneg (mul_nonneg hâ‚„ hâ‚„) h
        Â· simp; exact hâ‚†
      Â· simp
    apply hâ‚ƒ.right at hâ‚‰
    simp at hâ‚‰
    exact not_lt_of_ge hâ‚‰ hâ‚‡

theorem ofReal_mul {x y} : ğ“œ.ofReal (x * y) = ğ“œ.ofReal x * ğ“œ.ofReal y := by
  wlog h : 0 < x generalizing x y
  Â· simp at h
    rcases lt_or_eq_of_le h with h | h
    Â· apply neg_pos_of_neg at h
      apply this (y := y) at h
      simp [ofReal_neg, neg_mul] at h
      exact h
    Â· simp [h, ofReal_zero]
  wlog h' : 0 < y generalizing x y
  Â· simp at h'
    rcases lt_or_eq_of_le h' with h' | h'
    Â· apply neg_pos_of_neg at h'
      apply this h at h'
      simp [ofReal_neg, neg_mul] at h'
      exact h'
    Â· simp [h', ofReal_zero]
  apply ofReal_isLUB.unique
  constructor
  Â· intro z; simp; intro q hâ‚ hâ‚‚; subst hâ‚‚
    apply le_of_forall_rat_lt_imp_le
    intro r hâ‚‚; simp at hâ‚‚
    by_cases hâ‚ƒ : r > 0
    Â· let Î´ := âˆš(x * y / r)
      have hâ‚„ : 1 < Î´ := by
        simp [Î´, Real.lt_sqrt]; rw [lt_div_iff]
        Â· simp; exact lt_of_lt_of_le (Rat.cast_lt.mpr hâ‚‚) hâ‚
        Â· simp [hâ‚ƒ]
      rcases exists_rat_btwn (div_lt_self h hâ‚„) with âŸ¨râ‚, hâ‚…, hâ‚†âŸ©
      rcases exists_rat_btwn (div_lt_self h' hâ‚„) with âŸ¨râ‚‚, hâ‚‡, hâ‚ˆâŸ©
      trans râ‚ * râ‚‚
      Â· have := mul_lt_mul'' hâ‚… hâ‚‡
          (div_nonneg (le_of_lt h) (_root_.le_trans _root_.zero_le_one (le_of_lt hâ‚„)))
          (div_nonneg (le_of_lt h') (_root_.le_trans _root_.zero_le_one (le_of_lt hâ‚„)))
        rw [â†mul_div_mul_comm] at this
        simp [Î´] at this
        rw [â†Real.sqrt_mul, Real.sqrt_mul_self, div_div_cancel'] at this
        Â· simp [â†Rat.cast_mul] at this
          rw [â†Rat.cast_mul, Rat.cast_le]
          exact le_of_lt this
        Â· simp [ne_of_gt h, ne_of_gt h']
        Â· apply div_nonneg (mul_nonneg (le_of_lt h) (le_of_lt h'))
          simp; exact le_of_lt hâ‚ƒ
        Â· apply div_nonneg (mul_nonneg (le_of_lt h) (le_of_lt h'))
          simp; exact le_of_lt hâ‚ƒ
      Â· apply mul_le_mul
        Â· rw [â†ofReal_rat, ofReal_le]; exact le_of_lt hâ‚†
        Â· rw [â†ofReal_rat, ofReal_le]; exact le_of_lt hâ‚ˆ
        Â· simp; rw [â†Rat.cast_le (K := â„), Rat.cast_zero]
          apply le_trans' (le_of_lt hâ‚‡)
          exact div_nonneg (le_of_lt h') (_root_.le_trans _root_.zero_le_one (le_of_lt hâ‚„))
        Â· rw [â†ofReal_zero, ofReal_le]; exact le_of_lt h
    Â· simp at hâ‚ƒ
      trans 0
      Â· simp; exact hâ‚ƒ
      Â· apply mul_nonneg <;> rw [â†ofReal_zero, ofReal_le] <;> apply le_of_lt <;> assumption
  Â· intro a hâ‚; simp [upperBounds] at hâ‚
    apply le_of_forall_rat_lt_imp_le
    intro q hâ‚‚
    by_cases hâ‚ƒ : q > 0
    Â· rcases exists_sqrt (ğ“œ.ofReal x * ğ“œ.ofReal y / q : ğ“œ)
        (by
          apply div_nonneg
          Â· apply mul_nonneg <;> rw [â†ofReal_zero, ofReal_le] <;> apply le_of_lt <;> assumption
          Â· simp; exact le_of_lt hâ‚ƒ) with âŸ¨Î´, hâ‚„, hâ‚…âŸ©
      have hâ‚† : 1 < Î´ := by
        rw [â†pow_lt_pow_iff_left (n := 2) (by simp) hâ‚„ (by simp)]
        rw [hâ‚…, lt_div_iff]
        Â· simp; exact hâ‚‚
        Â· simp; exact hâ‚ƒ
      rcases exists_rat_btwn (div_lt_self (a := ğ“œ.ofReal x) (by rw [â†ofReal_zero, ofReal_lt]; exact h) hâ‚†) with âŸ¨râ‚, hâ‚‡, hâ‚ˆâŸ©
      rcases exists_rat_btwn (div_lt_self (a := ğ“œ.ofReal y) (by rw [â†ofReal_zero, ofReal_lt]; exact h') hâ‚†) with âŸ¨râ‚‚, hâ‚‰, hâ‚â‚€âŸ©
      trans râ‚ * râ‚‚
      Â· have := mul_lt_mul'' hâ‚‡ hâ‚‰
          (div_nonneg (by rw [â†ofReal_zero, ofReal_le]; exact le_of_lt h) (_root_.le_trans _root_.zero_le_one (le_of_lt hâ‚†)))
          (div_nonneg (by rw [â†ofReal_zero, ofReal_le]; exact le_of_lt h') (_root_.le_trans _root_.zero_le_one (le_of_lt hâ‚†)))
        rw [â†mul_div_mul_comm, â†pow_two, hâ‚…, div_div_cancel'] at this
        Â· exact le_of_lt this
        Â· apply ne_of_gt; apply mul_pos <;> rw [â†ofReal_zero, ofReal_lt] <;> assumption
      Â· rw [â†Rat.cast_mul]; apply hâ‚; simp
        apply mul_le_mul
        Â· rw [â†ofReal_rat, ofReal_lt] at hâ‚ˆ; exact le_of_lt hâ‚ˆ
        Â· rw [â†ofReal_rat, ofReal_lt] at hâ‚â‚€; exact le_of_lt hâ‚â‚€
        Â· simp; rw [â†Rat.cast_le (K := ğ“œ), Rat.cast_zero]
          apply le_trans' (le_of_lt hâ‚‰)
          apply div_nonneg
          Â· rw [â†ofReal_zero, ofReal_le]; exact le_of_lt h'
          Â· exact (_root_.le_trans _root_.zero_le_one (le_of_lt hâ‚†))
        Â· exact le_of_lt h
    Â· simp at hâ‚ƒ
      trans 0
      Â· simp; exact hâ‚ƒ
      Â· rw [â†Rat.cast_zero]; apply hâ‚; simp; exact mul_nonneg (le_of_lt h) (le_of_lt h')

theorem ofReal_inv : ğ“œ.ofReal xâ»Â¹ = (ğ“œ.ofReal x)â»Â¹ := by
  by_cases h : x = 0
  Â· simp [h, ofReal_zero]
  Â· rw [â†mul_eq_one_iff_eq_invâ‚€, â†ofReal_mul, inv_mul_cancel h, ofReal_one]
    intro h'; rw [â†ofReal_zero] at h'; exact h (ofReal_injective h')

end Model

namespace Real

noncomputable def model_iso_ğ“¡ (ğ“œ : Real.Model) : ğ“¡ â‰ƒá´¹ ğ“œ.toStructure where
  toEquiv := Equiv.ofBijective ğ“œ.ofReal âŸ¨ğ“œ.ofReal_injective, ğ“œ.ofReal_surjectiveâŸ©
  on_func
  | .zero, v => by simp [Vec.eq_nil]; apply ğ“œ.ofReal_zero
  | .one, v => by simp [Vec.eq_nil]; apply ğ“œ.ofReal_one
  | .add, v => by rw [Vec.eq_two (_ âˆ˜ _)]; apply ğ“œ.ofReal_add
  | .neg, v => by rw [Vec.eq_one (_ âˆ˜ _)]; apply ğ“œ.ofReal_neg
  | .mul, v => by rw [Vec.eq_two (_ âˆ˜ _)]; apply ğ“œ.ofReal_mul
  | .inv, v => by rw [Vec.eq_one (_ âˆ˜ _)]; apply ğ“œ.ofReal_inv
  on_rel
  | .le, v => by rw [Vec.eq_two (_ âˆ˜ _)]; symm; apply ğ“œ.ofReal_le

noncomputable def categorical : Real.Categorical
| ğ“œâ‚, ğ“œâ‚‚ => .trans (.symm (model_iso_ğ“¡ ğ“œâ‚)) (model_iso_ğ“¡ ğ“œâ‚‚)

end SecondOrder.Language.Theory.Real
