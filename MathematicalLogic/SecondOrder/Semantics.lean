import MathematicalLogic.SecondOrder.Syntax

universe u

namespace SecondOrder.Language

structure Structure (L : Language) where
  Dom : Type u
  interpFunc : L.Func n â†’ Vec Dom n â†’ Dom
  interpRel : L.Rel n â†’ Vec Dom n â†’ Prop

variable {L : Language}

namespace Structure

variable {M : L.Structure}

instance : CoeSort L.Structure (Type u) := âŸ¨(Â·.Dom)âŸ©

@[reducible] def interpTy (M : L.Structure) : Ty â†’ Type u
| Ty.var => M
| Ty.func n => Vec M n â†’ M
| Ty.rel n => Vec M n â†’ Prop

def Assignment (M: L.Structure) (Î“ : List Ty) := â¦ƒT : Tyâ¦„ â†’ Î“.Fin T â†’ M.interpTy T

def Assignment.nil : M.Assignment [] := by rintro _ âŸ¨âŸ©
notation "[]â‚" => Assignment.nil

def Assignment.cons (u : M.interpTy T) (Ï : M.Assignment Î“) : M.Assignment (T :: Î“)
| _, .fz => u
| _, .fs x => Ï x
infixr:80 " âˆ·â‚ " => Assignment.cons

def interp (M : L.Structure) {Î“} (Ï : M.Assignment Î“) : L.Term Î“ â†’ M
| #x => Ï x
| f â¬á¶  v => M.interpFunc f Î» i => M.interp Ï (v i)
| f â¬á¶ áµ› v => Ï f Î» i => M.interp Ï (v i)
notation:80 "âŸ¦ " t " âŸ§â‚œ " M ", " Ï:80 => interp M Ï t
@[simp] theorem interp_var : âŸ¦ #x âŸ§â‚œ M, Ï = Ï x := rfl
@[simp] theorem interp_fconst : âŸ¦ f â¬á¶  v âŸ§â‚œ M, Ï = M.interpFunc f Î» i => âŸ¦ v i âŸ§â‚œ M, Ï := rfl
@[simp] theorem interp_fvar : âŸ¦ f â¬á¶ áµ› v âŸ§â‚œ M, Ï = Ï f Î» i => âŸ¦ v i âŸ§â‚œ M, Ï := rfl

def satisfy (M : L.Structure) (Ï : M.Assignment Î“) : L.Formula Î“ â†’ Prop
| r â¬Ê³ v => M.interpRel r Î» i => âŸ¦ v i âŸ§â‚œ M, Ï
| r â¬Ê³áµ› v => Ï r Î» i => âŸ¦ v i âŸ§â‚œ M, Ï
| tâ‚ â‰ tâ‚‚ => âŸ¦ tâ‚ âŸ§â‚œ M, Ï = âŸ¦ tâ‚‚ âŸ§â‚œ M, Ï
| âŠ¥ => False
| p â‡’ q => M.satisfy Ï p â†’ M.satisfy Ï q
| âˆ€' p => âˆ€ (u : M), M.satisfy (u âˆ·â‚ Ï) p
| âˆ€á¶  n p => âˆ€ (f : Vec M n â†’ M), M.satisfy (f âˆ·â‚ Ï) p
| âˆ€Ê³ n p => âˆ€ (r : Vec M n â†’ Prop), M.satisfy (r âˆ·â‚ Ï) p
notation:50 M " âŠ¨[" Ï "] " p:50 => satisfy M Ï p
@[simp] theorem satisfy_rconst : M âŠ¨[Ï] r â¬Ê³ v â†” M.interpRel r Î» i => âŸ¦ v i âŸ§â‚œ M, Ï := by rfl
@[simp] theorem satisfy_rvar : M âŠ¨[Ï] r â¬Ê³áµ› v â†” Ï r Î» i => âŸ¦ v i âŸ§â‚œ M, Ï := by rfl
@[simp] theorem satisfy_eq : M âŠ¨[Ï] tâ‚ â‰ tâ‚‚ â†” âŸ¦ tâ‚ âŸ§â‚œ M, Ï = âŸ¦ tâ‚‚ âŸ§â‚œ M, Ï := by rfl
@[simp] theorem satisfy_false : Â¬ M âŠ¨[Ï] âŠ¥ := by tauto
@[simp] theorem satisfy_imp : M âŠ¨[Ï] p â‡’ q â†” M âŠ¨[Ï] p â†’ M âŠ¨[Ï] q := by rfl
@[simp] theorem satisfy_true : M âŠ¨[Ï] âŠ¤ := by tauto
@[simp] theorem satisfy_neg : M âŠ¨[Ï] ~ p â†” Â¬ M âŠ¨[Ï] p := by rfl
@[simp] theorem satisfy_and : M âŠ¨[Ï] p â©‘ q â†” M âŠ¨[Ï] p âˆ§ M âŠ¨[Ï] q := by simp [ClassicalPropNotation.and_def]
@[simp] theorem satisfy_or : M âŠ¨[Ï] p â©’ q â†” M âŠ¨[Ï] p âˆ¨ M âŠ¨[Ï] q := by simp [ClassicalPropNotation.or_def]; tauto
@[simp] theorem satisfy_iff : M âŠ¨[Ï] p â‡” q â†” (M âŠ¨[Ï] p â†” M âŠ¨[Ï] q) := by simp [ClassicalPropNotation.iff_def]; tauto
@[simp] theorem satisfy_all : M âŠ¨[Ï] âˆ€' p â†” âˆ€ (u : M), M âŠ¨[u âˆ·â‚ Ï] p := by rfl
@[simp] theorem satisfy_allf : M âŠ¨[Ï] âˆ€á¶  n p â†” âˆ€ (f : Vec M n â†’ M), M âŠ¨[f âˆ·â‚ Ï] p := by rfl
@[simp] theorem satisfy_allr : M âŠ¨[Ï] âˆ€Ê³ n p â†” âˆ€ (r : Vec M n â†’ Prop), M âŠ¨[r âˆ·â‚ Ï] p := by rfl
@[simp] theorem satisfy_ex : M âŠ¨[Ï] âˆƒ' p â†” âˆƒ (u : M), M âŠ¨[u âˆ·â‚ Ï] p := by simp [Formula.ex]
@[simp] theorem satisfy_exf : M âŠ¨[Ï] âˆƒá¶  n p â†” âˆƒ (f : Vec M n â†’ M), M âŠ¨[f âˆ·â‚ Ï] p := by simp [Formula.exf]
@[simp] theorem satisfy_exr : M âŠ¨[Ï] âˆƒÊ³ n p â†” âˆƒ (r : Vec M n â†’ Prop), M âŠ¨[r âˆ·â‚ Ï] p := by simp [Formula.exr]

abbrev satisfySentence (M : L.Structure) (p : L.Sentence) :=
  M âŠ¨[[]â‚] p
infix:50 " âŠ¨â‚› " => satisfySentence

structure Embedding (M : L.Structure) (N : L.Structure) extends M â†ª N where
  on_func : âˆ€ (f : L.Func n) (v : Vec M n), toEmbedding (M.interpFunc f v) = N.interpFunc f (toEmbedding âˆ˜ v)
  on_rel : âˆ€ (r : L.Rel n) (v : Vec M n), M.interpRel r v â†” N.interpRel r (toEmbedding âˆ˜ v)
infixr:25 " â†ªá´¹ " => Embedding

namespace Embedding

instance : CoeFun (M â†ªá´¹ N) (Î» _ => M â†’ N) := âŸ¨(Â·.toEmbedding)âŸ©

def refl : M â†ªá´¹ M where
  toEmbedding := .refl M
  on_func f v := rfl
  on_rel r v := by rfl

def trans (eâ‚ : M â†ªá´¹ N) (eâ‚‚ : N â†ªá´¹ ð“¢) : M â†ªá´¹ ð“¢ where
  toEmbedding := .trans eâ‚.toEmbedding eâ‚‚.toEmbedding
  on_func f v := by simp [eâ‚.on_func, eâ‚‚.on_func]; rfl
  on_rel r v := by rw [eâ‚.on_rel, eâ‚‚.on_rel]; rfl

end Embedding

structure Isomorphism (M : L.Structure) (N : L.Structure) extends M â‰ƒ N where
  on_func : âˆ€ (f : L.Func n) (v : Vec M n), toEquiv (M.interpFunc f v) = N.interpFunc f (toEquiv âˆ˜ v)
  on_rel : âˆ€ (r : L.Rel n) (v : Vec M n), M.interpRel r v â†” N.interpRel r (toEquiv âˆ˜ v)
infix:25 " â‰ƒá´¹ " => Isomorphism

namespace Isomorphism

instance : CoeFun (M â‰ƒá´¹ N) (Î» _ => M â†’ N) := âŸ¨(Â·.toEquiv)âŸ©

def refl : M â‰ƒá´¹ M where
  toEquiv := .refl M
  on_func f v := rfl
  on_rel r v := by rfl

def symm (i : M â‰ƒá´¹ N) : N â‰ƒá´¹ M where
  toEquiv := .symm i.toEquiv
  on_func f v := by apply i.toEquiv.injective; rw [i.on_func]; simp [Function.comp_def]
  on_rel r v := by rw [i.on_rel]; simp [Function.comp_def]

def trans (iâ‚ : M â‰ƒá´¹ N) (iâ‚‚ : N â‰ƒá´¹ ð“¢) : M â‰ƒá´¹ ð“¢ where
  toEquiv := .trans iâ‚.toEquiv iâ‚‚.toEquiv
  on_func f v := by simp [iâ‚.on_func, iâ‚‚.on_func]; rfl
  on_rel r v := by rw [iâ‚.on_rel, iâ‚‚.on_rel]; rfl

def toEmbedding (i : M â‰ƒá´¹ N) : M â†ªá´¹ N where
  toEmbedding := i.toEquiv
  on_func := i.on_func
  on_rel := i.on_rel

def onTy (i : M â‰ƒá´¹ N) : {T : Ty} â†’ M.interpTy T â†’ N.interpTy T
| .var, x => i x
| .func _, f => Î» v => i (f (i.symm âˆ˜ v))
| .rel _, r => Î» v => r (i.symm âˆ˜ v)

def onAssignment (i : M â‰ƒá´¹ N) : M.Assignment Î“ â†’ N.Assignment Î“ :=
  Î» Ï _ x => i.onTy (Ï x)

theorem on_term (i : M â‰ƒá´¹ N) (t : L.Term Î“) (Ï : M.Assignment Î“) :
  i (âŸ¦tâŸ§â‚œ M, Ï) = âŸ¦tâŸ§â‚œ N, i.onAssignment Ï := by
  induction t with simp [onAssignment, onTy]
  | fconst f v ih => rw [i.on_func]; congr; ext; simp [ih]
  | fvar f v ih => congr!; simp [â†ih, symm]

theorem on_formula (i : M â‰ƒá´¹ N) (p : L.Formula Î“) (Ï : M.Assignment Î“) :
  M âŠ¨[Ï] p â†” N âŠ¨[i.onAssignment Ï] p := by
  induction p with simp [onAssignment, onTy]
  | rconst r v => rw [i.on_rel]; congr!; simp [i.on_term]
  | rvar r v => congr!; simp [â†i.on_term, symm]
  | eq tâ‚ tâ‚‚ => simp [â†i.on_term]
  | imp p q ihâ‚ ihâ‚‚ => simp [ihâ‚, ihâ‚‚]
  | all p ih =>
    simp [ih]
    rw [i.toEquiv.forall_congr]
    congr!
    funext _ x; cases x <;> simp [onAssignment, onTy, Assignment.cons]
  | allf n p ih =>
    constructor
    Â· intro h f
      have := h Î» v => i.symm (f (i âˆ˜ v))
      simp [ih] at this
      convert this
      funext _ x; cases x <;> simp [onAssignment, onTy, Assignment.cons, symm, Function.comp_def]
    Â· intro h f
      have := h Î» v => i (f (i.symm âˆ˜ v))
      simp [ih]
      convert this
      funext _ x; cases x <;> simp [onAssignment, onTy, Assignment.cons]
  | allr n p ih =>
    constructor
    Â· intro h r
      have := h Î» v => r (i âˆ˜ v)
      simp [ih] at this
      convert this
      funext _ x; cases x <;> simp [onAssignment, onTy, Assignment.cons, symm, Function.comp_def]
    Â· intro h r
      have := h Î» v => r (i.symm âˆ˜ v)
      simp [ih]
      convert this
      funext _ x; cases x <;> simp [onAssignment, onTy, Assignment.cons]

end Isomorphism

end Structure

namespace Theory

structure Model (T : L.Theory) extends L.Structure where
  satisfy_theory : âˆ€ p âˆˆ T, toStructure âŠ¨â‚› p

instance {T : L.Theory} : CoeOut T.Model L.Structure := âŸ¨(Â·.toStructure)âŸ©
instance {T : L.Theory} : CoeSort T.Model (Type u) := âŸ¨(Â·.Dom)âŸ©

def Entails (T : L.Theory) (p : L.Sentence) :=
  âˆ€ (M : Model.{u} T), M âŠ¨â‚› p
infix:50 " âŠ¨ " => Entails

def Categorical (T : L.Theory) :=
  âˆ€ (M : T.Model) (N : T.Model), M â‰ƒá´¹ N.toStructure

end SecondOrder.Language.Theory
