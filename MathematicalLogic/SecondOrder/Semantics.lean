import MathematicalLogic.SecondOrder.Syntax

universe u v

namespace SecondOrder.Language

class IsStructure (L : Language) (M : Type u) where
  interpFunc : L.Func n â†’ Vec M n â†’ M
  interpRel : L.Rel n â†’ Vec M n â†’ Prop

variable {L : Language}

def interpTy (M : Type u) : Ty â†’ Type u
| .var => M
| .func n => Vec M n â†’ M
| .rel n => Vec M n â†’ Prop

def Assignment (M : Type u) (l : List Ty) := â¦ƒT : Tyâ¦„ â†’ l.Fin T â†’ interpTy M T

namespace Assignment

def nil : Assignment M [] := nofun
notation "[]â‚" => Assignment.nil

def cons (u : interpTy M T) (Ï : Assignment M l) : Assignment M (T :: l)
| _, .fz => u
| _, .fs x => Ï x
infixr:80 " âˆ·â‚ " => Assignment.cons

@[simp] theorem cons_zero : (u âˆ·â‚ Ï) 0 = u := rfl
@[simp] theorem cons_one {Ï : Assignment M (T :: l)} : (u âˆ·â‚ Ï) 1 = (Ï 0 : interpTy M T) := rfl
@[simp] theorem cons_two {Ï : Assignment M (Tâ‚ :: Tâ‚‚ :: l)} : (u âˆ·â‚ Ï) 2 = (Ï 1 : interpTy M Tâ‚‚) := rfl
@[simp] theorem cons_three {Ï : Assignment M (Tâ‚ :: Tâ‚‚ :: Tâ‚ƒ :: l)} : (u âˆ·â‚ Ï) 3 = (Ï 2 : interpTy M Tâ‚ƒ) := rfl
@[simp] theorem cons_four {Ï : Assignment M (Tâ‚ :: Tâ‚‚ :: Tâ‚ƒ :: Tâ‚„ :: l)} : (u âˆ·â‚ Ï) 4 = (Ï 3 : interpTy M Tâ‚„) := rfl

end Assignment

open IsStructure

variable {M : Type u} [IsStructure L M] {l} {Ï : Assignment M l} {p q : L.Formula l}

def interp (M : Type u) [L.IsStructure M] (Ï : Assignment M l) : L.Term l â†’ M
| #x => Ï x
| f â¬á¶  v => interpFunc f Î» i => interp M Ï (v i)
| f â¬á¶ áµ› v => Ï f Î» i => interp M Ï (v i)
notation:80 "âŸ¦ " t " âŸ§â‚œ " M ", " Ï:80 => interp M Ï t

@[simp] theorem interp_var : âŸ¦ (#x : L.Term l) âŸ§â‚œ M, Ï = Ï x := rfl
@[simp] theorem interp_fconst : âŸ¦ (f â¬á¶  v : L.Term l) âŸ§â‚œ M, Ï = interpFunc f Î» i => âŸ¦ v i âŸ§â‚œ M, Ï := rfl
@[simp] theorem interp_fvar : âŸ¦ (f â¬á¶ áµ› v : L.Term l) âŸ§â‚œ M, Ï = Ï f Î» i => âŸ¦ v i âŸ§â‚œ M, Ï := rfl

def satisfy (M : Type u) [L.IsStructure M] : {l : List Ty} â†’ L.Formula l â†’ Assignment M l â†’ Prop
| _, r â¬Ê³ v, Ï => interpRel r Î» i => âŸ¦ v i âŸ§â‚œ M, Ï
| _, r â¬Ê³áµ› v, Ï => Ï r Î» i => âŸ¦ v i âŸ§â‚œ M, Ï
| _, tâ‚ â‰ tâ‚‚, Ï => âŸ¦ tâ‚ âŸ§â‚œ M, Ï = âŸ¦ tâ‚‚ âŸ§â‚œ M, Ï
| _, âŠ¥, _ => False
| _, p â‡’ q, Ï => satisfy M p Ï â†’ satisfy M q Ï
| _, âˆ€' p, Ï => âˆ€ (u : M), satisfy M p (u âˆ·â‚ Ï)
| _, âˆ€á¶ [n] p, Ï => âˆ€ (f : Vec M n â†’ M), satisfy M p (f âˆ·â‚ Ï)
| _, âˆ€Ê³[n] p, Ï => âˆ€ (r : Vec M n â†’ Prop), satisfy M p (r âˆ·â‚ Ï)
notation:50 M " âŠ¨[" Ï "] " p:50 => satisfy M p Ï

@[simp] theorem satisfy_rconst : M âŠ¨[Ï] (r â¬Ê³ v : L.Formula l) â†” interpRel r Î» i => âŸ¦ v i âŸ§â‚œ M, Ï := by rfl
@[simp] theorem satisfy_rvar : M âŠ¨[Ï] (r â¬Ê³áµ› v : L.Formula l) â†” Ï r Î» i => âŸ¦ v i âŸ§â‚œ M, Ï := by rfl
@[simp] theorem satisfy_eq : M âŠ¨[Ï] (tâ‚ â‰ tâ‚‚ : L.Formula l) â†” âŸ¦ tâ‚ âŸ§â‚œ M, Ï = âŸ¦ tâ‚‚ âŸ§â‚œ M, Ï := by rfl
@[simp] theorem satisfy_false : Â¬ M âŠ¨[Ï] (âŠ¥ : L.Formula l) := by tauto
@[simp] theorem satisfy_imp : M âŠ¨[Ï] p â‡’ q â†” M âŠ¨[Ï] p â†’ M âŠ¨[Ï] q := by rfl
@[simp] theorem satisfy_true : M âŠ¨[Ï] (âŠ¤ : L.Formula l) := by tauto
@[simp] theorem satisfy_neg : M âŠ¨[Ï] ~ p â†” Â¬ M âŠ¨[Ï] p := by rfl
@[simp] theorem satisfy_and : M âŠ¨[Ï] p â©‘ q â†” M âŠ¨[Ï] p âˆ§ M âŠ¨[Ï] q := by simp [ClassicalPropNotation.and_def]
@[simp] theorem satisfy_or : M âŠ¨[Ï] p â©’ q â†” M âŠ¨[Ï] p âˆ¨ M âŠ¨[Ï] q := by simp [ClassicalPropNotation.or_def]; tauto
@[simp] theorem satisfy_iff : M âŠ¨[Ï] p â‡” q â†” (M âŠ¨[Ï] p â†” M âŠ¨[Ï] q) := by simp [ClassicalPropNotation.iff_def]; tauto
@[simp] theorem satisfy_all {p : L.Formula (_ :: l)} : M âŠ¨[Ï] âˆ€' p â†” âˆ€ (u : M), M âŠ¨[u âˆ·â‚ Ï] p := by rfl
@[simp] theorem satisfy_allf {p : L.Formula (_ :: l)} : M âŠ¨[Ï] âˆ€á¶ [n] p â†” âˆ€ (f : Vec M n â†’ M), M âŠ¨[f âˆ·â‚ Ï] p := by rfl
@[simp] theorem satisfy_allr {p : L.Formula (_ :: l)} : M âŠ¨[Ï] âˆ€Ê³[n] p â†” âˆ€ (r : Vec M n â†’ Prop), M âŠ¨[r âˆ·â‚ Ï] p := by rfl
@[simp] theorem satisfy_ex {p : L.Formula (_ :: l)} : M âŠ¨[Ï] âˆƒ' p â†” âˆƒ (u : M), M âŠ¨[u âˆ·â‚ Ï] p := by simp [Formula.ex]
@[simp] theorem satisfy_exf {p : L.Formula (_ :: l)} : M âŠ¨[Ï] âˆƒá¶ [n] p â†” âˆƒ (f : Vec M n â†’ M), M âŠ¨[f âˆ·â‚ Ï] p := by simp [Formula.exf]
@[simp] theorem satisfy_exr {p : L.Formula (_ :: l)} : M âŠ¨[Ï] âˆƒÊ³[n] p â†” âˆƒ (r : Vec M n â†’ Prop), M âŠ¨[r âˆ·â‚ Ï] p := by simp [Formula.exr]

abbrev satisfys (M : Type u) [L.IsStructure M] (p : L.Sentence) := M âŠ¨[[]â‚] p
infix:50 " âŠ¨â‚› " => satisfys

structure Structure (L : Language) where
  Dom : Type u
  interpFunc : L.Func n â†’ Vec Dom n â†’ Dom
  interpRel : L.Rel n â†’ Vec Dom n â†’ Prop

namespace Structure

instance : CoeSort L.Structure (Type u) := âŸ¨(Â·.Dom)âŸ©
instance {M : L.Structure} : L.IsStructure M := âŸ¨M.interpFunc, M.interpRelâŸ©
@[reducible] def of (M : Type u) [L.IsStructure M] : L.Structure := âŸ¨M, IsStructure.interpFunc, IsStructure.interpRelâŸ©

variable {M N : L.Structure}

structure Embedding (M : L.Structure) (N : L.Structure) extends M â†ª N where
  on_func : âˆ€ (f : L.Func n) (v : Vec M n), toEmbedding (IsStructure.interpFunc f v) = IsStructure.interpFunc f (toEmbedding âˆ˜ v)
  on_rel : âˆ€ (r : L.Rel n) (v : Vec M n), IsStructure.interpRel r v â†” IsStructure.interpRel r (toEmbedding âˆ˜ v)
infixr:25 " â†ªá´¹ " => Embedding

namespace Embedding

instance : CoeFun (M â†ªá´¹ N) (Î» _ => M â†’ N) := âŸ¨(Â·.toEmbedding)âŸ©

def refl : M â†ªá´¹ M where
  toEmbedding := .refl M
  on_func f v := rfl
  on_rel r v := by rfl

def trans (eâ‚ : M â†ªá´¹ N) (eâ‚‚ : N â†ªá´¹ ð“¢) : M â†ªá´¹ ð“¢ where
  toEmbedding := .trans eâ‚.toEmbedding eâ‚‚.toEmbedding
  on_func f v := by simp [eâ‚.on_func, eâ‚‚.on_func]; rfl
  on_rel r v := by rw [eâ‚.on_rel, eâ‚‚.on_rel]; rfl

end Embedding

structure Isomorphism (M : L.Structure) (N : L.Structure) extends M â‰ƒ N where
  on_func : âˆ€ (f : L.Func n) (v : Vec M n), toEquiv (IsStructure.interpFunc f v) = IsStructure.interpFunc f (toEquiv âˆ˜ v)
  on_rel : âˆ€ (r : L.Rel n) (v : Vec M n), IsStructure.interpRel r v â†” IsStructure.interpRel r (toEquiv âˆ˜ v)
infix:25 " â‰ƒá´¹ " => Isomorphism

namespace Isomorphism

instance : CoeFun (M â‰ƒá´¹ N) (Î» _ => M â†’ N) := âŸ¨(Â·.toEquiv)âŸ©

def refl : M â‰ƒá´¹ M where
  toEquiv := .refl M
  on_func f v := rfl
  on_rel r v := by rfl

def symm (i : M â‰ƒá´¹ N) : N â‰ƒá´¹ M where
  toEquiv := .symm i.toEquiv
  on_func f v := by apply i.toEquiv.injective; rw [i.on_func]; simp [Function.comp_def]
  on_rel r v := by rw [i.on_rel]; simp [Function.comp_def]

def trans (iâ‚ : M â‰ƒá´¹ N) (iâ‚‚ : N â‰ƒá´¹ ð“¢) : M â‰ƒá´¹ ð“¢ where
  toEquiv := .trans iâ‚.toEquiv iâ‚‚.toEquiv
  on_func f v := by simp [iâ‚.on_func, iâ‚‚.on_func]; rfl
  on_rel r v := by rw [iâ‚.on_rel, iâ‚‚.on_rel]; rfl

def toEmbedding (i : M â‰ƒá´¹ N) : M â†ªá´¹ N where
  toEmbedding := i.toEquiv
  on_func := i.on_func
  on_rel := i.on_rel

def onTy (i : M â‰ƒá´¹ N) : {T : Ty} â†’ interpTy M T â†’ interpTy N T
| .var, x => i x
| .func _, f => Î» v => i (f (i.symm âˆ˜ v))
| .rel _, r => Î» v => r (i.symm âˆ˜ v)

def onAssignment (i : M â‰ƒá´¹ N) : Assignment M l â†’ Assignment N l :=
  Î» Ï _ x => i.onTy (Ï x)

theorem on_term (i : M â‰ƒá´¹ N) (t : L.Term l) (Ï : Assignment M l) :
  i (âŸ¦tâŸ§â‚œ M, Ï) = âŸ¦tâŸ§â‚œ N, i.onAssignment Ï := by
  induction t with simp [onAssignment, onTy]
  | fconst f v ih => rw [i.on_func]; congr; ext; simp [ih]
  | fvar f v ih => congr!; simp [â†ih, symm]

theorem on_formula (i : M â‰ƒá´¹ N) (p : L.Formula l) (Ï : Assignment M l) :
  M âŠ¨[Ï] p â†” N âŠ¨[i.onAssignment Ï] p := by
  induction p with simp [onAssignment, onTy]
  | rconst r v => rw [i.on_rel]; congr!; simp [i.on_term]
  | rvar r v => congr!; simp [â†i.on_term, symm]
  | eq tâ‚ tâ‚‚ => simp [â†i.on_term]
  | imp p q ihâ‚ ihâ‚‚ => simp [ihâ‚, ihâ‚‚]
  | all p ih =>
    simp [ih]
    rw [i.toEquiv.forall_congr]
    congr!
    funext _ x; cases x <;> simp [onAssignment, onTy, Assignment.cons]
  | allf n p ih =>
    constructor
    Â· intro h f
      have := h Î» v => i.symm (f (i âˆ˜ v))
      simp [ih] at this
      convert this
      funext _ x; cases x <;> simp [onAssignment, onTy, Assignment.cons, symm, Function.comp_def]
    Â· intro h f
      have := h Î» v => i (f (i.symm âˆ˜ v))
      simp [ih]
      convert this
      funext _ x; cases x <;> simp [onAssignment, onTy, Assignment.cons]
  | allr n p ih =>
    constructor
    Â· intro h r
      have := h Î» v => r (i âˆ˜ v)
      simp [ih] at this
      convert this
      funext _ x; cases x <;> simp [onAssignment, onTy, Assignment.cons, symm, Function.comp_def]
    Â· intro h r
      have := h Î» v => r (i.symm âˆ˜ v)
      simp [ih]
      convert this
      funext _ x; cases x <;> simp [onAssignment, onTy, Assignment.cons]

end Isomorphism

end Structure

namespace Theory

class IsModel (T : L.Theory) (M : Type u) [L.IsStructure M] : Prop where
  satisfy_theory : âˆ€ p âˆˆ T, M âŠ¨â‚› p

structure Model (T : L.Theory) extends L.Structure where
  satisfy_theory :
    haveI : L.IsStructure toStructure := Structure.instIsStructureDom
    âˆ€ p âˆˆ T, toStructure âŠ¨â‚› p

variable {T : L.Theory} {M : T.Model} {p q : L.Sentence}

namespace Model

instance : CoeOut T.Model L.Structure := âŸ¨(Â·.toStructure)âŸ©
instance : CoeSort T.Model (Type u) := âŸ¨(Â·.Dom)âŸ©
instance : T.IsModel M := âŸ¨M.satisfy_theoryâŸ©

@[reducible] def of (M : Type u) [L.IsStructure M] [T.IsModel M] : T.Model := âŸ¨Structure.of M, IsModel.satisfy_theoryâŸ©

end Model

def Categorical (T : L.Theory) :=
  âˆ€ (M : Model.{u} T) (N : Model.{v} T), Nonempty (M â‰ƒá´¹ N.toStructure)

end SecondOrder.Language.Theory
