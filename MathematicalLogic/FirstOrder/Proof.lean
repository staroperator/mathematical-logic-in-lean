import Mathlib.Data.Set.Finite
import MathematicalLogic.FirstOrder.Syntax

namespace FirstOrder.Language

variable {ğ“› : Language}

abbrev Context (ğ“› : Language) := Set ğ“›.Formula

def Context.append (Î“ : ğ“›.Context) (p) := insert p Î“
infixl:51 ",, " => Context.append

def Context.lift : ğ“›.Context â†’ ğ“›.Context := Î» Î“ => {â†‘â‚šp | p âˆˆ Î“}
prefix:max "â†‘á¶œ" => Context.lift
theorem Context.lift_append : â†‘á¶œ(Î“,, p) = â†‘á¶œÎ“,, â†‘â‚šp := Set.image_insert_eq

inductive Axioms (ğ“› : Language) : ğ“›.Context where
| a1 {p q : ğ“›.Formula} :
  ğ“›.Axioms (p â‡’ (q â‡’ p))
| a2 {p q r : ğ“›.Formula} :
  ğ“›.Axioms ((p â‡’ q â‡’ r) â‡’ (p â‡’ q) â‡’ p â‡’ r)
| a3 {p q : ğ“›.Formula} :
  ğ“›.Axioms ((~ p â‡’ ~ q) â‡’ q â‡’ p)
| a4 {t : ğ“›.Term} {p : ğ“›.Formula} :
  ğ“›.Axioms (âˆ€' p â‡’ p[â†¦â‚› t]â‚š)
| a5 {p : ğ“›.Formula} :
  ğ“›.Axioms (p â‡’ âˆ€' â†‘â‚šp)
| a6 {p q : ğ“›.Formula} :
  ğ“›.Axioms (âˆ€' (p â‡’ q) â‡’ âˆ€' p â‡’ âˆ€' q)
| e1 {t : ğ“›.Term} :
  ğ“›.Axioms (t â‰ t)
| e2 {tâ‚ tâ‚‚ : ğ“›.Term} {p : ğ“›.Formula} :
  ğ“›.Axioms ((tâ‚ â‰ tâ‚‚) â‡’ p[â†¦â‚› tâ‚]â‚š â‡’ p[â†¦â‚› tâ‚‚]â‚š)
| all {p : ğ“›.Formula} :
  ğ“›.Axioms p â†’ ğ“›.Axioms (âˆ€' p)

theorem Axioms.closed_under_subst : p âˆˆ ğ“›.Axioms â†’ p[Ïƒ]â‚š âˆˆ ğ“›.Axioms := by
  intro h
  induction h generalizing Ïƒ
    <;> simp [Formula.subst_swap, Formula.shift_subst_lift]
  case all ih => exact all ih
  all_goals constructor

inductive Proof (Î“ : ğ“›.Context) : ğ“›.Formula â†’ Prop where
| hyp : p âˆˆ Î“ â†’ Proof Î“ p
| ax : p âˆˆ ğ“›.Axioms â†’ Proof Î“ p
| mp : Proof Î“ (p â‡’ q) â†’ Proof Î“ p â†’ Proof Î“ q
infix:50 " âŠ¢ " => Proof

namespace Proof

variable {Î“ Î” : ğ“›.Context} {t tâ‚ tâ‚‚ : ğ“›.Term} {p q r : ğ“›.Formula}

theorem hyp_append : Î“,, p âŠ¢ p :=
  hyp (Set.mem_insert _ _)

theorem weaken : Î“ âŠ† Î” â†’ Î“ âŠ¢ p â†’ Î” âŠ¢ p := by
  intros hâ‚ hâ‚‚
  induction hâ‚‚ with
  | hyp h => exact hyp (hâ‚ h)
  | ax h => exact ax h
  | mp _ _ ihâ‚ ihâ‚‚ => exact mp ihâ‚ ihâ‚‚

theorem weaken_append : Î“ âŠ¢ p â†’ Î“,, q âŠ¢ p :=
  weaken (Set.subset_insert _ _)

theorem mp2 : Î“ âŠ¢ p â‡’ q â‡’ r â†’ Î“ âŠ¢ p â†’ Î“ âŠ¢ q â†’ Î“ âŠ¢ r :=
  Î» hâ‚ hâ‚‚ hâ‚ƒ => mp (mp hâ‚ hâ‚‚) hâ‚ƒ

theorem identity : Î“ âŠ¢ p â‡’ p :=
  mp2 (ax .a2) (ax .a1) (ax (.a1 (q := p)))

theorem deduction : Î“ âŠ¢ p â‡’ q â†” Î“,, p âŠ¢ q := by
  constructor
  Â· intro h; exact mp (weaken_append h) hyp_append
  Â· intro h
    induction h with
    | hyp h =>
      cases h with
      | inl h => subst h; exact identity
      | inr h => exact mp (ax .a1) (hyp h)
    | ax h => exact mp (ax .a1) (ax h)
    | mp _ _ ihâ‚ ihâ‚‚ => exact mp (mp (ax .a2) ihâ‚) ihâ‚‚

theorem cut : Î“ âŠ¢ p â†’ Î“,, p âŠ¢ q â†’ Î“ âŠ¢ q :=
  Î» hâ‚ hâ‚‚ => mp (deduction.mpr hâ‚‚) hâ‚

lemma closed_under_subst : Î“ âŠ¢ p â†’ { q[Ïƒ]â‚š | q âˆˆ Î“ } âŠ¢ p[Ïƒ]â‚š := by
  intro h
  induction h with
  | hyp h => apply hyp; exists _, h
  | ax h => exact ax (Axioms.closed_under_subst h)
  | mp _ _ ihâ‚ ihâ‚‚ => exact mp ihâ‚ ihâ‚‚

theorem closed_under_shift : Î“ âŠ¢ p â†’ â†‘á¶œÎ“ âŠ¢ â†‘â‚šp := closed_under_subst

macro "pintro" : tactic => `(tactic| apply deduction.mpr)
elab "pintros" n:(ppSpace colGt num)? : tactic => do
  match n with
  | some n =>
    for _ in [:n.getNat] do
      Lean.Elab.Tactic.evalTactic (â†`(tactic| pintro))
  | none =>
    Lean.Elab.Tactic.evalTactic (â†`(tactic| repeat pintro))

private def hypTerm (n : â„•) : Lean.MacroM (Lean.TSyntax `term) := do
  let mut t â† `(hyp_append)
  for _ in [:n] do
    t â† `(weaken_append $t)
  return t

macro "passumption" n:(ppSpace colGt num)? : tactic => do
  match n with
  | some n => `(tactic| exact $(â† hypTerm n.getNat))
  | none => `(tactic| (apply hyp; repeat first | exact Or.inl rfl | apply Or.inr))

macro "phave" t:(ppSpace colGt term) : tactic =>
  `(tactic| refine cut (p := $t) ?_ ?_)
macro "psuffices" t:(ppSpace colGt term) : tactic =>
  `(tactic| (refine cut (p := $t) ?_ ?_; swap))

/--
  Unify `Î“ âŠ† Î”` as `Î“, pâ‚, â‹¯, pâ‚™ = Î”`.
  Return `some n` if succeed, and `none` if fail. -/
private partial def isSubsetOf (Î“ Î” : Lean.Expr) : Lean.MetaM (Option â„•) := do
  let s â† Lean.MonadBacktrack.saveState
  if â† Lean.Meta.isDefEq Î“ Î” then
    return some 0
  Lean.MonadBacktrack.restoreState s
  if let some (_, Î”', _) := Î”.app3? ``Context.append then
    if let some n := â† isSubsetOf Î“ Î”' then
      return some (n + 1)
  return none

/--
  Given a proof term of `Î“ âŠ¢ pâ‚ â‡’ â‹¯ â‡’ pâ‚™`, apply it on the goal `Î“' âŠ¢ pâ‚™` through MP.
  `Î“` should be a subset of `Î“'`.
  
  Control the application depth `n` through `with` clause. -/
elab "papply" t:(ppSpace colGt term) d:((" with " num)?) : tactic =>
  Lean.Elab.Tactic.withMainContext do
    let appTerm â† Lean.Elab.Term.elabTerm t none
    let appType â† Lean.instantiateMVars (â† Lean.Meta.inferType appTerm)
    let (forallMVars, _, type) â† Lean.Meta.forallMetaTelescopeReducing appType
    let (Lean.mkApp3 (.const ``Proof []) ğ“› Î“ p) := type
      | throwError m!"{type} is not a proof"
    let goal â† Lean.Elab.Tactic.getMainGoal
    let goalType â† goal.getType'
    let some (ğ“›', Î”, _) := goalType.app3? ``Proof
      | throwError m!"{goalType} is not a proof"
    let true := â† Lean.Meta.isDefEq ğ“› ğ“›'
      | throwError m!"failed to unify {ğ“›} and {ğ“›'}"
    let some weakenDepth := â† isSubsetOf Î“ Î”
      | throwError m!"failed to unify {Î“} as a subset of {Î”}"
    let weakenTy â† do
      let weakenTy := Lean.mkApp3
        (.const ``Set.Subset [0]) (.app (.const ``Formula []) ğ“›) Î“ Î”
      Lean.Meta.checkTypeIsProp weakenTy
      pure weakenTy
    let weakenTerm â† do
      let mut weakenTerm â† `(Set.Subset.refl _)
      for _ in [:weakenDepth] do
        weakenTerm := â† `(Set.Subset.trans $weakenTerm (Set.subset_insert _ _))
      Lean.Elab.Term.elabTerm weakenTerm (some weakenTy)
    let mut proofTerm := Lean.mkApp6
      (.const ``weaken []) ğ“› Î“ Î” p weakenTerm
      (Lean.mkAppN appTerm forallMVars)
    let mut newMVarIds := []
    let mut goalFormula := p
    let maxDepth := d.raw.getArgs[1]?.map (Â·.toNat)
    repeat do
      let proofType â† Lean.Meta.inferType proofTerm
      if â† Lean.Meta.isDefEq goalType proofType then
        goal.assign proofTerm
        break
      if let some d := maxDepth then
        if newMVarIds.length >= d then
          throwError "failed to apply {appType} at {goalType} within depth {d}"
      else if let some (_, p, q) := (â† Lean.Meta.whnf goalFormula).app3? ``Formula.imp then
        let mvarId â† Lean.mkFreshMVarId
        newMVarIds := newMVarIds ++ [mvarId]
        let mvar â† Lean.Meta.mkFreshExprMVarWithId mvarId (some (
          Lean.mkApp3 (.const ``Proof []) ğ“› Î” p))
        let newProofTerm := Lean.mkApp6
          (.const ``mp []) ğ“› Î” p q proofTerm mvar
        proofTerm := newProofTerm
        goalFormula := q
      else
        throwError "failed to apply {appType} at {goalType}"
    for mvar in forallMVars do
      if let (.mvar mvarid) := mvar then
        if !(â† mvarid.isAssigned) then
          newMVarIds := newMVarIds ++ [mvarid]
    Lean.Elab.Tactic.replaceMainGoal newMVarIds

/-- Apply an assumption through MP. -/
macro "papplya" n:(ppSpace colGt num) : tactic => do
  `(tactic| papply $(â† hypTerm n.getNat))

/-- Close the goal with given proof term. -/
macro "pexact" t:(ppSpace colGt term) : tactic =>
  `(tactic| papply $t with 0)

theorem composition : Î“ âŠ¢ (p â‡’ q) â‡’ (q â‡’ r) â‡’ p â‡’ r := by
  pintros
  papplya 1
  papplya 2
  passumption

theorem contraposition : Î“ âŠ¢ (p â‡’ q) â‡’ ~ q â‡’ ~ p := composition

theorem contraposition2 : Î“ âŠ¢ (p â‡’ ~ q) â‡’ q â‡’ ~ p := by
  pintros
  papplya 2 <;> passumption

theorem true_intro : Î“ âŠ¢ âŠ¤ := identity

theorem false_elim : Î“ âŠ¢ âŠ¥ â‡’ p := by
  papply ax .a3
  pintro
  exact true_intro

theorem contradiction : Î“ âŠ¢ ~ p â‡’ p â‡’ q := by
  pintros
  papply false_elim
  papplya 1
  passumption

theorem double_neg1 : Î“ âŠ¢ p â‡’ ~ ~ p := by
  pintros
  papplya 0
  passumption

theorem double_neg2 : Î“ âŠ¢ ~ ~ p â‡’ p := by
  pintro
  papply ax .a3
  Â· exact double_neg1
  Â· passumption

theorem contraposition3 : Î“ âŠ¢ (~ p â‡’ q) â‡’ ~ q â‡’ p := by
  papply composition
  Â· exact contraposition
  Â· papply ax .a2
    pintro
    exact double_neg2

theorem not_imp_left : Î“ âŠ¢ ~ (p â‡’ q) â‡’ p := by
  pintro
  papply double_neg2
  papply contraposition
  Â· exact contradiction (q := q)
  Â· passumption

theorem not_imp_right : Î“ âŠ¢ ~ (p â‡’ q) â‡’ ~ q := by
  papply contraposition
  exact ax .a1

theorem and_intro : Î“ âŠ¢ p â‡’ q â‡’ p â©‘ q := by
  pintros
  papplya 0 <;> passumption

theorem and_left : Î“ âŠ¢ p â©‘ q â‡’ p := by
  pintro
  papply double_neg2
  pintro
  papplya 1
  pintros
  papply false_elim
  papplya 2
  passumption

theorem and_right : Î“ âŠ¢ p â©‘ q â‡’ q := by
  pintro
  papply double_neg2
  pintro
  papplya 1
  pintro
  passumption

theorem or_inl : Î“ âŠ¢ p â‡’ p â©’ q := by
  pintros
  papply contradiction <;> passumption

theorem or_inr : Î“ âŠ¢ q â‡’ p â©’ q := ax .a1

theorem or_elim : Î“ âŠ¢ p â©’ q â‡’ (p â‡’ r) â‡’ (q â‡’ r) â‡’ r := by
  pintros
  papply double_neg2
  pintro
  papplya 0
  papplya 2
  psuffices ~ p
  Â· papply contradiction
    Â· passumption 1
    Â· papplya 2
      papplya 4
      passumption
  Â· pintro
    papplya 1
    papplya 3
    passumption

theorem excluded_middle : Î“ âŠ¢ ~ p â©’ p := double_neg2

theorem andN_intro {v : Vec ğ“›.Formula n} :
  (âˆ€ i, Î“ âŠ¢ v i) â†’ Î“ âŠ¢ â‹€i, v i := by
  intro h
  induction n with
  | zero => exact true_intro
  | succ n ih =>
    papply and_intro
    Â· apply h
    Â· apply ih; intro i; apply h

theorem andN_elim {v : Vec ğ“›.Formula n} {i : Fin n} :
  (Î“ âŠ¢ â‹€i, v i) â†’ Î“ âŠ¢ v i := by
  intro h
  induction n with
  | zero => exact i.elim0
  | succ n ih =>
    cases i using Fin.cases with
    | zero => exact mp and_left h
    | succ i => apply ih (mp and_right h)

theorem iff_intro : Î“ âŠ¢ (p â‡’ q) â‡’ (q â‡’ p) â‡’ (p â‡” q) := and_intro
theorem iff_mp : Î“ âŠ¢ (p â‡” q) â‡’ (p â‡’ q) := and_left
theorem iff_mpr : Î“ âŠ¢ (p â‡” q) â‡’ (q â‡’ p) := and_right
theorem iff_iff : Î“ âŠ¢ p â‡” q â†’ (Î“ âŠ¢ p â†” Î“ âŠ¢ q) := Î» h => âŸ¨iff_mp.mp2 h, iff_mpr.mp2 hâŸ©

theorem iff_refl : Î“ âŠ¢ p â‡” p := mp2 iff_intro identity identity

theorem iff_symm : Î“ âŠ¢ (p â‡” q) â‡’ (q â‡” p) := by
  pintro
  papply iff_intro
  Â· papply iff_mpr; passumption
  Â· papply iff_mp; passumption

theorem iff_trans : Î“ âŠ¢ (p â‡” q) â‡’ (q â‡” r) â‡’ (p â‡” r) := by
  pintros 2
  papply iff_intro
  Â· papply composition <;> papply iff_mp <;> passumption
  Â· papply composition <;> papply iff_mpr <;> passumption

theorem iff_congr_imp : Î“ âŠ¢ (pâ‚ â‡” pâ‚‚) â‡’ (qâ‚ â‡” qâ‚‚) â‡’ ((pâ‚ â‡’ qâ‚) â‡” (pâ‚‚ â‡’ qâ‚‚)) := by
  pintros 2
  papply iff_intro <;> pintros
  Â· papply iff_mp; passumption
    papplya 1
    papply iff_mpr; passumption
    passumption
  Â· papply iff_mpr; passumption
    papplya 1
    papply iff_mp; passumption
    passumption

theorem generalization : â†‘á¶œÎ“ âŠ¢ p â†’ Î“ âŠ¢ âˆ€' p := by
  intro h
  induction h with
  | hyp h =>
    rcases h with âŸ¨p, âŸ¨hâ‚, hâ‚‚âŸ©âŸ©
    subst hâ‚‚
    exact mp (ax .a5) (hyp hâ‚)
  | ax h => exact ax (.all h)
  | mp _ _ ihâ‚ ihâ‚‚ => exact mp2 (ax .a6) ihâ‚ ihâ‚‚

theorem generalization_iff : â†‘á¶œÎ“ âŠ¢ p â†” Î“ âŠ¢ âˆ€' p := by
  constructor
  Â· exact generalization
  Â· intro h
    apply closed_under_shift at h
    simp [Formula.shift] at h
    apply mp (ax $ .a4 (t := #0)) at h
    have : p[â‡‘â‚›Subst.shift]â‚š[â†¦â‚› #0]â‚š = p := by
      rw [â†Formula.subst_comp]
      conv => rhs; rw [â†Formula.subst_id (p := p)]
      congr; funext x; cases x <;> simp
    rw [this] at h
    exact h

theorem iff_congr_forall : Î“ âŠ¢ âˆ€' (p â‡” q) â‡’ âˆ€' p â‡” âˆ€' q := by
  pintro
  papply iff_intro <;> papply ax .a6 <;> rw [â†deduction] <;> papply ax .a6
    <;> apply generalization
  Â· exact iff_mp
  Â· exact iff_mpr

theorem not_forall : Î“ âŠ¢ ~ âˆ€' p â‡’ âˆƒ' (~ p) := by
  papply contraposition
  papply ax .a6
  apply generalization
  exact double_neg2

theorem not_exists : Î“ âŠ¢ ~ âˆƒ' p â‡’ âˆ€' (~ p) := double_neg2

theorem forall_elim : Î“ âŠ¢ âˆ€' p â‡’ p[â†¦â‚› t]â‚š := ax .a4

theorem exists_intro : Î“ âŠ¢ p[â†¦â‚› t]â‚š â‡’ âˆƒ' p := by
  pintros
  suffices _ âŠ¢ (~ p)[â†¦â‚› t]â‚š by
    papply this
    passumption
  papply ax .a4
  passumption

theorem exists_elim : Î“ âŠ¢ âˆƒ' p â‡’ (âˆ€' (p â‡’ â†‘â‚šq)) â‡’ q := by
  pintros
  papply double_neg2
  pintros
  papplya 2
  suffices _ âŠ¢ âˆ€' (â†‘â‚š(~ q) â‡’ ~ p) by
    papply ax .a6
    Â· exact this
    Â· papply ax .a5
      passumption
  papply ax .a6
  Â· apply generalization
    exact contraposition
  Â· passumption

theorem exists_self : Î“ âŠ¢ âˆƒ' â†‘â‚šp â‡’ p := by
  papply contraposition3
  apply ax .a5

theorem foralls_elim {p : ğ“›.BFormula m} :
  Î“ âŠ¢ (âˆ€* p).val â‡’ p.val[Ïƒ]â‚š := by
  induction' m with m ih generalizing Ïƒ
  Â· rw [Sentence.val_subst_eq]; exact identity
  Â· let Ïƒ' := Î» x => Ïƒ (x + 1)
    papply composition
    Â· exact ih (Ïƒ := Ïƒ')
    Â· have h : â‡‘â‚›Ïƒ' âˆ˜â‚› â†¦â‚› (Ïƒ 0) = Ïƒ := by
        funext x; cases x <;> simp [Term.shift_subst_single]
      conv in _[Ïƒ]â‚š => rw [â†h, Formula.subst_comp]
      apply Proof.forall_elim

theorem foralls_elim_self {p : ğ“›.BFormula m} :
  Î“ âŠ¢ (âˆ€* p).val â‡’ p.val := by
  rw [â†Formula.subst_id (p := p.val)]
  exact foralls_elim

theorem refl : Î“ âŠ¢ t â‰ t := ax .e1
macro "prefl" : tactic => `(tactic| pexact refl)

theorem subst : Î“ âŠ¢ tâ‚ â‰ tâ‚‚ â‡’ p[â†¦â‚› tâ‚]â‚š â‡’ p[â†¦â‚› tâ‚‚]â‚š := ax .e2

theorem symm : Î“ âŠ¢ tâ‚ â‰ tâ‚‚ â‡’ tâ‚‚ â‰ tâ‚ := by
  have h := @subst _ Î“ tâ‚ tâ‚‚ (#0 â‰ â†‘â‚œtâ‚)
  simp [Term.shift_subst_single] at h
  pintro
  papply h
  Â· passumption
  Â· prefl
macro "psymm" : tactic => `(tactic| papply symm)

theorem trans : Î“ âŠ¢ tâ‚ â‰ tâ‚‚ â‡’ tâ‚‚ â‰ tâ‚ƒ â‡’ tâ‚ â‰ tâ‚ƒ := by
  have h := @subst _ Î“ tâ‚‚ tâ‚ (#0 â‰ â†‘â‚œtâ‚ƒ)
  simp [Term.shift_subst_single] at h
  pintros
  papply h
  Â· psymm; passumption
  Â· passumption
macro "ptrans" t:(ppSpace colGt term)? : tactic =>
  match t with
  | some t => `(tactic| papply trans (tâ‚‚ := $t))
  | none => `(tactic| papply trans)

theorem subst_iff : Î“ âŠ¢ tâ‚ â‰ tâ‚‚ â‡’ p[â†¦â‚› tâ‚]â‚š â‡” p[â†¦â‚› tâ‚‚]â‚š := by
  pintro
  papply iff_intro <;> papply subst
  Â· passumption
  Â· psymm; passumption

theorem subst_term : Î“ âŠ¢ tâ‚ â‰ tâ‚‚ â‡’ t[â†¦â‚› tâ‚]â‚œ â‰ t[â†¦â‚› tâ‚‚]â‚œ := by
  pintro
  have h := @subst _ Î“ tâ‚ tâ‚‚ (â†‘â‚œ(t[â†¦â‚› tâ‚]â‚œ) â‰ t)
  simp [Term.shift_subst_single] at h
  papply h
  Â· passumption
  Â· prefl

theorem congr_func {vâ‚ vâ‚‚ : Vec ğ“›.Term n} :
  Î“ âŠ¢ (â‹€i, vâ‚ i â‰ vâ‚‚ i) â‡’ f â¬â‚œ vâ‚ â‰ f â¬â‚œ vâ‚‚ := by
  pintro
  suffices âˆ€ k â‰¤ n, _ âŠ¢ f â¬â‚œ vâ‚ â‰ f â¬â‚œ (Î» i => if i < k then vâ‚‚ i else vâ‚ i) by
    replace this := this n (by rfl)
    simp at this
    exact this
  intros k hâ‚
  induction k with
  | zero => simp; prefl
  | succ k ih =>
    ptrans
    Â· exact ih (Nat.le_of_succ_le hâ‚)
    Â· let k' : Fin n := âŸ¨k, hâ‚âŸ©
      let t := f â¬â‚œ (Î» i => if i < k then â†‘â‚œ(vâ‚‚ i) else if i = k then #0 else â†‘â‚œ(vâ‚ i))
      have hâ‚‚ : t[â†¦â‚› (vâ‚ k')]â‚œ = f â¬â‚œ (Î» i => if i < k then vâ‚‚ i else vâ‚ i) := by
        simp [t]; ext i
        rcases Nat.lt_trichotomy i k with (h | h | h)
        Â· simp [h, Term.shift_subst_single]
        Â· simp [h]; congr; apply Fin.eq_of_val_eq; simp [k', h]
        Â· simp [Nat.not_lt_of_gt h, Nat.ne_of_gt h, Term.shift_subst_single]
      have hâ‚ƒ : t[â†¦â‚› (vâ‚‚ k')]â‚œ = f â¬â‚œ (Î» i => if i < k.succ then vâ‚‚ i else vâ‚ i) := by
        simp [t]; ext i
        rcases Nat.lt_trichotomy i k with (h | h | h)
        Â· simp [h, Nat.lt_succ_of_lt h, Term.shift_subst_single]
        Â· simp [h]; congr; apply Fin.eq_of_val_eq; simp [k', h]
        Â· simp [Nat.not_lt_of_gt h, Nat.lt_succ, Nat.not_le_of_gt h,
            Nat.ne_of_gt h, Term.shift_subst_single]
      rw [â†hâ‚‚, â†hâ‚ƒ]
      papply subst_term
      apply andN_elim (v := Î» i => vâ‚ i â‰ vâ‚‚ i)
      passumption

theorem subst_term' :
  (âˆ€ i, Î“ âŠ¢ Ïƒâ‚ i â‰ Ïƒâ‚‚ i) â†’ Î“ âŠ¢ t[Ïƒâ‚]â‚œ â‰ t[Ïƒâ‚‚]â‚œ := by
  intro h
  induction t with simp
  | var => apply h
  | func f v ih => papply congr_func; apply andN_intro; exact ih

theorem congr_rel_iff {r} {vâ‚ vâ‚‚ : Vec ğ“›.Term n} :
  Î“ âŠ¢ (â‹€i, vâ‚ i â‰ vâ‚‚ i) â‡’ r â¬áµ£ vâ‚ â‡” r â¬áµ£ vâ‚‚ := by
  pintro
  suffices âˆ€ k â‰¤ n, _ âŠ¢ r â¬áµ£ vâ‚ â‡” r â¬áµ£ (Î» i => if i < k then vâ‚‚ i else vâ‚ i) by
    replace this := this n (by rfl)
    simp at this
    exact this
  intros k hâ‚
  induction k with
  | zero => simp; exact iff_refl
  | succ k ih =>
    papply iff_trans
    Â· exact ih (Nat.le_of_succ_le hâ‚)
    Â· let k' : Fin n := âŸ¨k, hâ‚âŸ©
      let p := r â¬áµ£ (Î» i => if i < k then â†‘â‚œ(vâ‚‚ i) else if i = k then #0 else â†‘â‚œ(vâ‚ i))
      have hâ‚‚ : p[â†¦â‚› (vâ‚ k')]â‚š = r â¬áµ£ (Î» i => if i < k then vâ‚‚ i else vâ‚ i) := by
        simp [p]; ext i
        rcases Nat.lt_trichotomy i k with (h | h | h)
        Â· simp [h, Term.shift_subst_single]
        Â· simp [h]; congr; apply Fin.eq_of_val_eq; simp [k', h]
        Â· simp [Nat.not_lt_of_gt h, Nat.ne_of_gt h, Term.shift_subst_single]
      have hâ‚ƒ : p[â†¦â‚› (vâ‚‚ k')]â‚š = r â¬áµ£ (Î» i => if i < k.succ then vâ‚‚ i else vâ‚ i) := by
        simp [p]; ext i
        rcases Nat.lt_trichotomy i k with (h | h | h)
        Â· simp [h, Nat.lt_succ_of_lt h, Term.shift_subst_single]
        Â· simp [h]; congr; apply Fin.eq_of_val_eq; simp [k', h]
        Â· simp [Nat.not_lt_of_gt h, Nat.lt_succ, Nat.not_le_of_gt h,
            Nat.ne_of_gt h, Term.shift_subst_single]
      rw [â†hâ‚‚, â†hâ‚ƒ]
      papply subst_iff
      apply andN_elim (v := Î» i => vâ‚ i â‰ vâ‚‚ i)
      passumption

theorem congr_rel {r} {vâ‚ vâ‚‚ : Vec ğ“›.Term n} :
  Î“ âŠ¢ (â‹€i, vâ‚ i â‰ vâ‚‚ i) â‡’ r â¬áµ£ vâ‚ â‡’ r â¬áµ£ vâ‚‚ := by
  pintro
  papply iff_mp
  papply congr_rel_iff
  passumption

theorem congr_eq : Î“ âŠ¢ tâ‚ â‰ tâ‚' â‡’ tâ‚‚ â‰ tâ‚‚' â‡’ tâ‚ â‰ tâ‚‚ â‡’ tâ‚' â‰ tâ‚‚' := by
  pintros
  ptrans
  Â· psymm; passumption
  Â· ptrans <;> passumption

theorem congr_eq_iff : Î“ âŠ¢ tâ‚ â‰ tâ‚' â‡’ tâ‚‚ â‰ tâ‚‚' â‡’ tâ‚ â‰ tâ‚‚ â‡” tâ‚' â‰ tâ‚‚' := by
  pintros 2
  papply iff_intro
  Â· papply congr_eq <;> passumption
  Â· papply congr_eq <;> psymm <;> passumption

theorem subst_iff' : (âˆ€ i, Î“ âŠ¢ Ïƒâ‚ i â‰ Ïƒâ‚‚ i) â†’ Î“ âŠ¢ p[Ïƒâ‚]â‚š â‡” p[Ïƒâ‚‚]â‚š := by
  intro h
  induction p generalizing Î“ Ïƒâ‚ Ïƒâ‚‚ with simp
  | rel r v =>
    papply congr_rel_iff; apply andN_intro; intro; apply subst_term'; exact h
  | eq tâ‚ tâ‚‚ =>
    papply congr_eq_iff <;> apply subst_term' <;> exact h
  | false =>
    exact iff_refl
  | imp p q ihâ‚ ihâ‚‚ =>
    papply iff_congr_imp <;> apply_assumption <;> exact h
  | all p ih =>
    papply iff_congr_forall; apply generalization; apply ih; intro i
    cases i with simp
    | zero => prefl
    | succ i => apply closed_under_shift (p := Ïƒâ‚ i â‰ Ïƒâ‚‚ i); apply h

theorem subst' : (âˆ€ i, Î“ âŠ¢ Ïƒâ‚ i â‰ Ïƒâ‚‚ i) â†’ Î“ âŠ¢ p[Ïƒâ‚]â‚š â‡’ p[Ïƒâ‚‚]â‚š := by
  intro h
  papply iff_mp
  exact subst_iff' h

namespace Rewrite

mutual
inductive Term (Î“ : ğ“›.Context) : ğ“›.Term â†’ ğ“›.Term â†’ Prop
| matched {tâ‚ tâ‚‚} : Î“ âŠ¢ tâ‚ â‰ tâ‚‚ â†’ Term Î“ tâ‚ tâ‚‚
| func : Terms Î“ vâ‚ vâ‚‚ â†’ Term Î“ (f â¬â‚œ vâ‚) (f â¬â‚œ vâ‚‚)
| refl {t} : Term Î“ t t
inductive Terms (Î“ : ğ“›.Context) : Vec ğ“›.Term n â†’ Vec ğ“›.Term n â†’ Prop
| cons {tâ‚ tâ‚‚} : Term Î“ tâ‚ tâ‚‚ â†’ Terms Î“ vâ‚ vâ‚‚ â†’ Terms Î“ (tâ‚ âˆ·áµ¥ vâ‚) (tâ‚‚ âˆ·áµ¥ vâ‚‚)
| refl : Terms Î“ v v
end

theorem Terms.term :
  âˆ€ {n} {vâ‚ vâ‚‚ : Vec ğ“›.Term n}, Terms Î“ vâ‚ vâ‚‚ â†’ âˆ€ i, Term Î“ (vâ‚ i) (vâ‚‚ i)
| _, _, _, cons hâ‚ hâ‚‚, i => i.cases hâ‚ (Î» _ => hâ‚‚.term _)
| _, _, _, refl, _ => Term.refl

theorem Term.sound : âˆ€ {tâ‚ tâ‚‚}, Term Î“ tâ‚ tâ‚‚ â†’ Î“ âŠ¢ tâ‚ â‰ tâ‚‚
| _, _, matched h => h
| _, _, func h => by
  papply congr_func; apply andN_intro; intro i; exact (h.term i).sound
| _, _, refl => Proof.refl

inductive Formula (Î“ : ğ“›.Context) : ğ“›.Formula â†’ ğ“›.Formula â†’ Prop
| rel {r vâ‚ vâ‚‚} : Terms Î“ vâ‚ vâ‚‚ â†’ Formula Î“ (r â¬áµ£ vâ‚) (r â¬áµ£ vâ‚‚)
| eq {tâ‚ tâ‚' tâ‚‚ tâ‚‚'} : Term Î“ tâ‚ tâ‚' â†’ Term Î“ tâ‚‚ tâ‚‚' â†’ Formula Î“ (tâ‚ â‰ tâ‚‚) (tâ‚' â‰ tâ‚‚')
| imp {pâ‚ qâ‚ pâ‚‚ qâ‚‚} : Formula Î“ pâ‚ qâ‚ â†’ Formula Î“ pâ‚‚ qâ‚‚ â†’ Formula Î“ (pâ‚ â‡’ pâ‚‚) (qâ‚ â‡’ qâ‚‚)
| refl {p} : Formula Î“ p p

theorem Formula.sound : Formula Î“ p q â†’ Î“ âŠ¢ p â‡” q := by
  intro h
  induction h with
  | rel h => papply congr_rel_iff; apply andN_intro; intro i; exact (h.term i).sound
  | eq => papply congr_eq_iff <;> apply Term.sound <;> assumption
  | imp => papply iff_congr_imp <;> assumption
  | refl => exact iff_refl

macro "prewrite" t:term : tactic =>
  `(tactic| (
    apply mp2 iff_mpr
    Â· apply Formula.sound
      repeat' first
      | apply Formula.rel | apply Formula.eq | apply Formula.imp | exact Formula.refl
      | apply Terms.cons | exact Terms.refl
      | exact Term.matched (by pexact $t) | apply Term.func | exact Term.refl
    try simp))

syntax rwRule := ("â† "?) term

/--
  Rewrite goal using given terms.
  If a number instead of term is given, it will use assumption in rewrite.
  
  Use `â†` to change the direction. -/
elab "prw" "[" rules:withoutPosition(rwRule,*,?) "]" : tactic => do
  for rule in rules.raw.getSepArgs do
    let t â†
      match rule with
      | `(rwRule | $n:num) => Lean.Elab.liftMacroM (hypTerm n.getNat)
      | `(rwRule | $t:term) => pure t
      | `(rwRule | â† $n:num) => `(mp symm $(â† Lean.Elab.liftMacroM (hypTerm n.getNat)))
      | `(rwRule | â† $t:term) => `(mp symm $t)
      | _ => throwError "this shouldn't happen"
    Lean.Elab.Tactic.evalTactic (â†`(tactic| prewrite $t))

end Rewrite

theorem compactness : Î“ âŠ¢ p â†’ âˆƒ Î”, Î” âŠ† Î“ âˆ§ Î”.Finite âˆ§ Î” âŠ¢ p := by
  intro h
  induction h with
  | @hyp p h =>
    exists {p}; simp [h]
    passumption; rfl
  | ax h =>
    exists âˆ…; simp
    exact ax h
  | mp _ _ ihâ‚ ihâ‚‚ =>
    rcases ihâ‚ with âŸ¨Î”â‚, hâ‚, hâ‚‚, hâ‚ƒâŸ©
    rcases ihâ‚‚ with âŸ¨Î”â‚‚, hâ‚„, hâ‚…, hâ‚†âŸ©
    exists Î”â‚ âˆª Î”â‚‚; simp [hâ‚, hâ‚„, hâ‚‚, hâ‚…]
    apply Proof.mp
    Â· apply weaken _ hâ‚ƒ; simp
    Â· apply weaken _ hâ‚†; simp

end Proof

notation Î“:50 "âŠ¬" p:50 => Â¬ Î“ âŠ¢ p

namespace Context

def Consistent (Î“ : ğ“›.Context) := Î“ âŠ¬ âŠ¥

theorem Consistent.weaken : Î“ âŠ† Î” â†’ Consistent Î” â†’ Consistent Î“ := by
  intros hâ‚ hâ‚‚ h
  apply hâ‚‚
  exact Proof.weaken hâ‚ h

theorem Consistent.append : Consistent (Î“,, p) â†” Î“ âŠ¬ ~ p := by
  constructor
  Â· intro hâ‚ hâ‚‚
    apply hâ‚
    rw [â†Proof.deduction]
    exact hâ‚‚
  Â· intro hâ‚ hâ‚‚
    apply hâ‚
    pintro
    exact hâ‚‚

end Language.Context
