import MathematicalLogic.FirstOrder.Completeness.Language

lemma Set.decompose_subset_union {sâ‚ sâ‚‚ sâ‚ƒ : Set Î±} :
  sâ‚ âŠ† sâ‚‚ âˆª sâ‚ƒ â†’ âˆƒ sâ‚„ sâ‚…, sâ‚ = sâ‚„ âˆª sâ‚… âˆ§ sâ‚„ âŠ† sâ‚‚ âˆ§ sâ‚… âŠ† sâ‚ƒ := by
  intros h
  exists sâ‚ âˆ© sâ‚‚
  exists sâ‚ âˆ© sâ‚ƒ
  aesop

def Fin.embedAt : (k : â„•) â†’ Fin (n + k + 1)
| 0 => 0
| k + 1 => (embedAt k).succ

def Fin.insertAt : (k : â„•) â†’ Fin (n + k) â†’ Fin (n + k + 1)
| 0, x => x.succ
| k + 1, x => x.cases 0 Î» x => (insertAt k x).succ

theorem Fin.embedAt_or_insertAt (x : Fin (n + k + 1)) : x = embedAt k âˆ¨ âˆƒ y, x = insertAt k y := by
  induction k with
  | zero =>
    cases x using Fin.cases with
    | zero => left; rfl
    | succ x => right; exists x
  | succ k ih =>
    cases x using Fin.cases with
    | zero => right; exists Fin.ofNat 0
    | succ x =>
      rcases ih x with h | âŸ¨y, hâŸ©
      Â· left; simp [h, embedAt]
      Â· right; exists y.succ; simp [h, insertAt]

namespace FirstOrder.Language

variable {ğ“› : Language}

def Term.consts : ğ“›.Term n â†’ Set ğ“›.Const
| #_ => {}
| .func (m := 0) c _ => {c}
| .func (m := _ + 1) _ v => â‹ƒi, (v i).consts

def Formula.consts : ğ“›.Formula n â†’ Set ğ“›.Const
| _ â¬Ê³ v => â‹ƒi, (v i).consts
| tâ‚ â‰ tâ‚‚ => tâ‚.consts âˆª tâ‚‚.consts
| âŠ¥ => {}
| p â‡’ q => p.consts âˆª q.consts
| âˆ€' p => p.consts

lemma Term.consts_of_subst :
  t[Ïƒ]â‚œ.consts = t.consts âˆª â‹ƒ x âˆˆ t.vars, (Ïƒ x).consts := by
  induction t with
  | var x => simp [vars, consts]
  | @func n f v ih =>
    cases n with simp [vars, consts]
    | succ => rw [Set.iUnion_comm, â†Set.iUnion_union_distrib]; simp_rw [ih]

lemma Formula.consts_of_subst {Ïƒ : ğ“›.Subst n m} :
  p[Ïƒ]â‚š.consts = p.consts âˆª â‹ƒ x âˆˆ p.free, (Ïƒ x).consts := by
  induction p generalizing m with simp_rw [free, consts]
  | rel r v => simp_rw [Set.biUnion_iUnion, â†Set.iUnion_union_distrib, Term.consts_of_subst]
  | eq tâ‚ tâ‚‚ => simp_rw [Set.biUnion_union, Term.consts_of_subst]; aesop
  | false => simp
  | imp p q ihâ‚ ihâ‚‚ => rw [ihâ‚, ihâ‚‚, Set.biUnion_union]; aesop
  | all p ih =>
    ext c; simp [ih]; apply or_congr_right
    constructor
    Â· rintro âŸ¨x, hâ‚, hâ‚‚âŸ©
      cases x using Fin.cases with
      | zero => contradiction
      | succ x =>
        simp [Term.shift, Term.consts_of_subst] at hâ‚‚
        rcases hâ‚‚ with (hâ‚‚ | âŸ¨_, _, hâ‚ƒâŸ©)
        Â· exists x
        Â· simp [Term.consts] at hâ‚ƒ
    Â· rintro âŸ¨y, âŸ¨hâ‚, hâ‚‚âŸ©âŸ©
      exists y.succ
      constructor
      Â· exact hâ‚
      Â· simp [Term.shift, Term.consts_of_subst]
        left; exact hâ‚‚

def Subst.singleAt : (k : â„•) â†’ (t : ğ“›.Term n) â†’ ğ“›.Subst (n + k + 1) (n + k)
| 0, t => â†¦â‚› t
| k + 1, t => â‡‘â‚› (singleAt k t)
local infix:55 " â†¦â‚› " => Subst.singleAt

theorem Subst.singleAt_const_app_embedAt {c : ğ“›.Const} : (k â†¦â‚› (c : ğ“›.Term n)) (Fin.embedAt k) = (c : ğ“›.Term _) := by
  induction k with simp [singleAt, Fin.embedAt]
  | succ k ih => simp [ih, Term.shift, Vec.eq_nil]

theorem Subst.singleAt_app_insertAt : (k â†¦â‚› t) (Fin.insertAt k x) = #x := by
  induction k with simp [singleAt, Fin.insertAt]
  | succ k ih => cases x using Fin.cases <;> simp [ih]

def Subst.shiftAt : (k : â„•) â†’ ğ“›.Subst (n + k) (n + k + 1)
| 0 => shift
| k + 1 => â‡‘â‚› (shiftAt k)

theorem Subst.shiftAt_app : shiftAt (ğ“› := ğ“›) k x = #(Fin.insertAt k x) := by
  induction k with simp [shiftAt, Fin.insertAt]
  | succ k ih => cases x using Fin.cases <;> simp [ih]

theorem Subst.shiftAt_comp_singleAt : shiftAt k âˆ˜â‚› (k â†¦â‚› t) = id := by
  funext x; simp
  induction k with simp [singleAt, shiftAt]
  | succ k ih => cases x using Fin.cases <;> simp [Term.shift_subst_lift, ih]

def Subst.insertAt : (k : â„•) â†’ ğ“›.Subst (n + k) m â†’ (t : ğ“›.Term m) â†’ ğ“›.Subst (n + k + 1) m
| 0, Ïƒ, t => t âˆ·áµ¥ Ïƒ
| k + 1, Ïƒ, t => Ïƒ.head âˆ·áµ¥ insertAt k Ïƒ.tail t

theorem Subst.insertAt_app_embedAt : insertAt k Ïƒ t (Fin.embedAt k) = t := by
  induction k with simp [insertAt, Fin.embedAt]
  | succ k ih => simp [ih]

theorem Subst.insertAt_app_insertAt : insertAt k Ïƒ t (Fin.insertAt k x) = Ïƒ x := by
  induction k with simp [insertAt, Fin.insertAt]
  | succ k ih => cases x using Fin.cases <;> simp [Vec.head, ih]

open Classical in
noncomputable def Term.invConst (k : â„•) : ğ“›.Term (n + k) â†’ ğ“›.Const â†’ ğ“›.Term (n + k + 1)
| #x, _ => #(Fin.insertAt k x)
| func (m := 0) f _, c => if f = c then #(Fin.embedAt k) else f â¬á¶  []áµ¥
| func (m := _ + 1) f v, c => f â¬á¶  Î» i => (v i).invConst k c

theorem Term.subst_singleAt_invConst {t : ğ“›.Term (n + k + 1)} (h : c âˆ‰ t.consts) :
  (t[k â†¦â‚› c]â‚œ).invConst k c = t := by
  induction t with simp
  | var x =>
    rcases Fin.embedAt_or_insertAt x with h | âŸ¨y, hâŸ©
    Â· simp [h, Subst.singleAt_const_app_embedAt, Term.invConst]
    Â· simp [h, Subst.singleAt_app_insertAt, Term.invConst]
  | @func m f v ih =>
    cases m <;> simp [invConst] <;> simp [Term.consts] at h
    Â· simp [Ne.symm h, Vec.eq_nil]
    Â· ext i; simp [ih i (h i)]

theorem Term.invConst_eq_shiftAt {t : ğ“›.Term (n + k)} (h : c âˆ‰ t.consts) :
  t.invConst k c = t[Subst.shiftAt k]â‚œ := by
  induction t with
  | var x => simp [invConst, Subst.shiftAt_app]
  | @func m f v ih =>
    cases m <;> simp [invConst] <;> simp [consts] at h
    Â· simp [Ne.symm h, Vec.eq_nil]
    Â· ext i; simp [ih i (h i)]

theorem Term.invConst_subst {t : ğ“›.Term (n + k)} {Ïƒ : ğ“›.Subst (n + k) (n' + k')} :
  (t[Ïƒ]â‚œ).invConst k' c = (t.invConst k c)[Subst.insertAt k ((Â·.invConst k' c) âˆ˜ Ïƒ) #(Fin.embedAt k')]â‚œ := by
  induction t with
  | var x => simp [invConst, Function.comp, Subst.insertAt_app_insertAt]
  | @func m f v ih =>
    cases m <;> simp [invConst]
    Â· by_cases h : f = c <;> simp [h, Vec.eq_nil, Subst.insertAt_app_embedAt]
    Â· simp [ih]

theorem Term.invConst_shift {t : ğ“›.Term (n + k)} :
  (â†‘â‚œt).invConst (k + 1) c = â†‘â‚œ(t.invConst k c) := by
  rw [shift, invConst_subst]
  congr; funext x; simp [Function.comp, invConst, Fin.embedAt]
  rcases Fin.embedAt_or_insertAt x with h | âŸ¨y, hâŸ©
  Â· simp [h, Subst.insertAt_app_embedAt]
  Â· simp [h, Subst.insertAt_app_insertAt, invConst, Fin.insertAt]

noncomputable def Formula.invConst (k : â„•) : ğ“›.Formula (n + k) â†’ ğ“›.Const â†’ ğ“›.Formula (n + k + 1)
| r â¬Ê³ v, c => r â¬Ê³ Î» i => (v i).invConst k c
| tâ‚ â‰ tâ‚‚, c => tâ‚.invConst k c â‰ tâ‚‚.invConst k c
| âŠ¥, _ => âŠ¥
| p â‡’ q, c => p.invConst k c â‡’ q.invConst k c
| âˆ€' p, c => âˆ€' (p.invConst (k + 1) c)
@[simp] theorem Formula.invConst_false : (âŠ¥ : ğ“›.Formula (n + k)).invConst k c = âŠ¥ := by
  rw [â†false_eq]; simp only [invConst]
@[simp] theorem Formula.invConst_imp : (p â‡’ q : ğ“›.Formula (n + k)).invConst k c = p.invConst k c â‡’ q.invConst k c := by
  rw [â†imp_eq]; simp only [invConst]
@[simp] theorem Formula.invConst_neg : (~ p : ğ“›.Formula (n + k)).invConst k c = ~ p.invConst k c := by
  rw [â†neg_eq, invConst_imp, invConst_false]; rfl

theorem Formula.invConst_andN {v : Vec (ğ“›.Formula (n + k)) m} : invConst k (â‹€ i, v i) c = â‹€ i, invConst k (v i) c := by
  induction m with simp [andN]
  | zero => simp [Top.top]
  | succ m ih => simp [PropNotation.and, Vec.head, Vec.tail, Function.comp_def, ih]

theorem Formula.subst_singleAt_invConst {p : ğ“›.Formula (n + k + 1)} (h : c âˆ‰ p.consts) :
  (p[k â†¦â‚› c]â‚š).invConst k c = p := by
  cases p with (simp [invConst] <;> simp [consts] at h)
  | rel r v => ext i; simp [Term.subst_singleAt_invConst (h i)]
  | eq tâ‚ tâ‚‚ => simp [Term.subst_singleAt_invConst h.left, Term.subst_singleAt_invConst h.right]
  | imp p q => simp [subst_singleAt_invConst h.left, subst_singleAt_invConst h.right]
  | all p => rw [â†Subst.singleAt, subst_singleAt_invConst (k := k + 1) h]

theorem Formula.invConst_eq_shiftAt {p : ğ“›.Formula (n + k)} (h : c âˆ‰ p.consts) :
  p.invConst k c = p[Subst.shiftAt k]â‚š := by
  cases p with (simp [invConst] <;> simp [consts] at h)
  | rel r v => ext i; simp [Term.invConst_eq_shiftAt (h i)]
  | eq tâ‚ tâ‚‚ => simp [Term.invConst_eq_shiftAt h.left, Term.invConst_eq_shiftAt h.right]
  | imp p q => simp [invConst_eq_shiftAt h.left, invConst_eq_shiftAt h.right]
  | all p => simp [invConst_eq_shiftAt (k := k + 1) h, Subst.shiftAt]

theorem Formula.invConst_subst {p : ğ“›.Formula (n + k)} {Ïƒ : ğ“›.Subst (n + k) (n' + k')} :
  (p[Ïƒ]â‚š).invConst k' c = (p.invConst k c)[Subst.insertAt k ((Â·.invConst k' c) âˆ˜ Ïƒ) #(Fin.embedAt k')]â‚š := by
  cases p with simp [invConst]
  | rel => ext; simp [Term.invConst_subst]
  | eq => simp [Term.invConst_subst]
  | imp p q => simp [invConst_subst (p := p), invConst_subst (p := q)]
  | all p =>
    rw [invConst_subst (k := k + 1) (p := p)]
    congr; funext x
    cases' x using Fin.cases with x
    Â· simp [Subst.insertAt, Vec.head, Term.invConst, Fin.insertAt]
    Â· simp [Subst.insertAt, Vec.tail, Function.comp, Fin.embedAt, Term.invConst]
      rcases Fin.embedAt_or_insertAt x with h | âŸ¨y, hâŸ©
      Â· simp [h, Subst.insertAt_app_embedAt]
      Â· simp [h, Subst.insertAt_app_insertAt, Term.invConst_shift]

theorem Formula.invConst_shift {p : ğ“›.Formula (n + k)} :
  (â†‘â‚šp).invConst (k + 1) c = â†‘â‚š(p.invConst k c) := by
  rw [shift, invConst_subst]
  congr; funext x; simp [Function.comp, invConst, Fin.embedAt]
  rcases Fin.embedAt_or_insertAt x with h | âŸ¨y, hâŸ©
  Â· simp [h, Subst.insertAt_app_embedAt]
  Â· simp [h, Subst.insertAt_app_insertAt, Term.invConst, Fin.insertAt]

theorem Formula.invConst_subst_single {p : ğ“›.Formula (n + k + 1)} {t : ğ“›.Term (n + k)} :
  (p[â†¦â‚› t]â‚š).invConst k c = (p.invConst (k + 1) c)[â†¦â‚› (t.invConst k c)]â‚š := by
  rw [invConst_subst (k := k + 1)]
  congr; clear p; funext x
  cases x using Fin.cases with
  | zero => simp [Subst.insertAt, Vec.head]
  | succ x =>
    simp [Subst.insertAt, Vec.tail, Function.comp, Term.invConst]
    rcases Fin.embedAt_or_insertAt x with h | âŸ¨y, hâŸ©
    Â· simp [h, Subst.insertAt_app_embedAt]
    Â· simp [h, Subst.insertAt_app_insertAt, Term.invConst]

lemma Axiom.inv_const {p : ğ“›.Formula (n + k)} :
  p âˆˆ ğ“›.Axiom â†’ Î“ âŠ¢ p.invConst k c := by
  intro h
  cases h with simp [Formula.invConst, Formula.invConst_andN, Formula.invConst_shift, Formula.invConst_subst_single]
  | @eq_congr_func m _ vâ‚ vâ‚‚ f =>
    cases m with
    | zero => by_cases h : f = c <;> simp [Term.invConst, h] <;> pintro <;> prefl
    | succ m => simp [Term.invConst]; exact Proof.ax eq_congr_func
  | all h => pintro; exact inv_const (k := k + 1) h
  | _ => apply Proof.ax; constructor

lemma Proof.inv_const {p : ğ“›.Formula (n + k)} (hâ‚ : âˆ€ p âˆˆ Î“, c âˆ‰ p.consts) :
  Î“ âŠ¢ p â†’ (Â·[Subst.shiftAt k]â‚š) '' Î“ âŠ¢ p.invConst k c := by
  intro h
  induction h with
  | @hyp p h => apply hyp; exists p, h; rw [Formula.invConst_eq_shiftAt (hâ‚ p h)]
  | ax h => exact Axiom.inv_const h
  | mp _ _ ihâ‚ ihâ‚‚ => simp at ihâ‚; exact mp ihâ‚ ihâ‚‚

theorem Proof.const_generalization {Î“ : ğ“›.FormulaSet n}
  (hâ‚ : âˆ€ p âˆˆ Î“, c âˆ‰ p.consts) (hâ‚‚ : c âˆ‰ p.consts) :
  Î“ âŠ¢ p[â†¦â‚› c]â‚š â†’ Î“ âŠ¢ âˆ€' p := by
  intro h
  apply inv_const (k := 0) (c := c) hâ‚ at h
  rw [â†Subst.singleAt, Formula.subst_singleAt_invConst (k := 0) hâ‚‚] at h
  exact forall_intro h



inductive henkinStep.Func (ğ“› : Language) (n : â„•) : â„• â†’ Type u
| inj : ğ“›.Func m â†’ Func ğ“› n m
| wit : ğ“›.Formula (n + 1) â†’ Func ğ“› n 0

def henkinStep (ğ“› : Language) (n : â„•) : Language where
  Func := henkinStep.Func ğ“› n
  Rel := ğ“›.Rel

namespace henkinStep

variable {ğ“› : Language}

def wit (p : ğ“›.Formula (n + 1)) : (ğ“›.henkinStep n).Const := .wit p

@[simps] def hom : ğ“› â†’á´¸ ğ“›.henkinStep n where
  onFunc f := .inj f
  onRel r := r

theorem wit_not_in_homTerm : wit p âˆ‰ (hom.onTerm t).consts := by
  induction t with simp [Hom.onTerm, Term.consts]
  | @func m f v ih =>
    cases m <;> simp [Term.consts]
    Â· simp [wit]
    Â· exact ih

theorem wit_not_in_homFormula : wit p âˆ‰ (hom.onFormula q).consts := by
  induction q with simp [Hom.onFormula, Formula.consts]
  | rel | eq => simp [wit_not_in_homTerm]
  | imp _ _ ihâ‚ ihâ‚‚ => simp [ihâ‚, ihâ‚‚]
  | all _ ih => simp [ih]

def invTerm : (k : â„•) â†’ (ğ“›.henkinStep n).Term (m + k) â†’ ğ“›.Term (m + k + 1)
| k, #x => #(Fin.insertAt k x)
| k, (.inj f) â¬á¶  v => f â¬á¶  Î» i => invTerm k (v i)
| k, (.wit _) â¬á¶  _ => #(Fin.embedAt k)

theorem invTerm_homTerm : invTerm k (hom.onTerm t : (ğ“›.henkinStep n).Term _) = t[Subst.shiftAt k]â‚œ := by
  induction t with simp [Hom.onTerm, invTerm]
  | var x => simp [Subst.shiftAt_app]
  | func f v ih => ext; simp [ih]

theorem invTerm_subst {Ïƒ : (ğ“›.henkinStep n).Subst (m + k) (m' + k')} :
  invTerm k' (t[Ïƒ]â‚œ) = (invTerm k t)[Subst.insertAt k ((invTerm k' Â·) âˆ˜ Ïƒ) #(Fin.embedAt k')]â‚œ := by
  induction t with
  | var x => simp [invTerm, Function.comp, Subst.insertAt_app_insertAt]
  | func f v ih =>
    cases f <;> simp [invTerm]
    Â· ext; simp [ih]
    Â· simp [Subst.insertAt_app_embedAt]

theorem invTerm_shift : invTerm (k + 1) (â†‘â‚œt) = â†‘â‚œ(invTerm k t) := by
  rw [Term.shift, invTerm_subst]
  congr; funext x; simp [Function.comp, invTerm, Fin.embedAt]
  rcases Fin.embedAt_or_insertAt x with h | âŸ¨y, hâŸ©
  Â· simp [h, Subst.insertAt_app_embedAt]
  Â· simp [h, Subst.insertAt_app_insertAt, invTerm, Fin.insertAt]

def invFormula : (k : â„•) â†’ (ğ“›.henkinStep n).Formula (m + k) â†’ ğ“›.Formula (m + k + 1)
| k, r â¬Ê³ v => r â¬Ê³ Î» i => invTerm k (v i)
| k, tâ‚ â‰ tâ‚‚ => invTerm k tâ‚ â‰ invTerm k tâ‚‚
| k, âŠ¥ => âŠ¥
| k, p â‡’ q => invFormula k p â‡’ invFormula k q
| k, âˆ€' p => âˆ€' (invFormula (k + 1) p)
@[simp] theorem invFormula_false : invFormula k (âŠ¥ : (ğ“›.henkinStep n).Formula (m + k)) = âŠ¥ := by
  rw [â†Formula.false_eq]; simp only [invFormula]
@[simp] theorem invFormula_imp {p q : (ğ“›.henkinStep n).Formula (m + k)} :
  invFormula k (p â‡’ q) = invFormula k p â‡’ invFormula k q := by
  rw [â†Formula.imp_eq]; simp only [invFormula]
@[simp] theorem invFormula_neg : invFormula k (~ p) = ~ invFormula k p := by
  rw [â†Formula.neg_eq, invFormula_imp, invFormula_false]; rfl

theorem invFormula_andN {v : Vec ((ğ“›.henkinStep n).Formula (m + k)) l} :
  invFormula k (â‹€ i, v i) = â‹€ i, invFormula k (v i) := by
  induction l with simp [Formula.andN]
  | zero => simp [Top.top]
  | succ m ih => simp [PropNotation.and, Vec.head, Vec.tail, Function.comp_def, ih]

theorem invFormula_homFormula : invFormula k (hom.onFormula p : (ğ“›.henkinStep n).Formula _) = p[Subst.shiftAt k]â‚š := by
  cases p with simp [Hom.onFormula, invFormula]
  | rel | eq => simp [invTerm_homTerm]
  | imp p q => simp [invFormula_homFormula (p := p), invFormula_homFormula (p := q)]
  | all p => simp [invFormula_homFormula (k := k + 1) (p := p), Subst.shiftAt]

theorem invFormula_subst {Ïƒ : (ğ“›.henkinStep n).Subst (m + k) (m' + k')} :
  invFormula k' (p[Ïƒ]â‚š) = (invFormula k p)[Subst.insertAt k ((invTerm k' Â·) âˆ˜ Ïƒ) #(Fin.embedAt k')]â‚š := by
  cases p with simp [Hom.onFormula, invFormula]
  | rel | eq => simp [invTerm_subst]
  | imp p q => simp [invFormula_subst (p := p), invFormula_subst (p := q)]
  | all p =>
    rw [invFormula_subst (k := k + 1) (p := p)]
    congr; funext x
    cases' x using Fin.cases with x
    Â· simp [Subst.insertAt, Vec.head, invTerm, Fin.insertAt]
    Â· simp [Subst.insertAt, Vec.tail, Function.comp, Fin.embedAt, Term.invConst]
      rcases Fin.embedAt_or_insertAt x with h | âŸ¨y, hâŸ©
      Â· simp [h, Subst.insertAt_app_embedAt]
      Â· simp [h, Subst.insertAt_app_insertAt, invTerm_shift]

theorem invFormula_shift : invFormula (k + 1) (â†‘â‚šp) = â†‘â‚š(invFormula k p) := by
  rw [Formula.shift, invFormula_subst]
  congr; funext x; simp [Function.comp, invTerm, Fin.embedAt]
  rcases Fin.embedAt_or_insertAt x with h | âŸ¨y, hâŸ©
  Â· simp [h, Subst.insertAt_app_embedAt]
  Â· simp [h, Subst.insertAt_app_insertAt, invTerm, Fin.insertAt]

theorem invFormula_subst_single : invFormula k (p[â†¦â‚› t]â‚š) = (invFormula (k + 1) p)[â†¦â‚› (invTerm k t)]â‚š := by
  rw [invFormula_subst (k := k + 1)]
  congr; clear p; funext x
  cases x using Fin.cases with
  | zero => simp [Subst.insertAt, Vec.head]
  | succ x =>
    simp [Subst.insertAt, Vec.tail, Function.comp, invTerm]
    rcases Fin.embedAt_or_insertAt x with h | âŸ¨y, hâŸ©
    Â· simp [h, Subst.insertAt_app_embedAt]
    Â· simp [h, Subst.insertAt_app_insertAt, invTerm]

theorem inv_axiom {p : (ğ“›.henkinStep n).Formula (m + k)} : p âˆˆ (ğ“›.henkinStep n).Axiom â†’ Î“ âŠ¢ invFormula k p := by
  intro h
  cases h with simp [invFormula, invFormula_andN, invFormula_shift, invFormula_subst_single]
  | @eq_congr_func m _ vâ‚ vâ‚‚ f =>
    cases m with
    | zero => cases f <;> simp [Vec.eq_nil] <;> pintro <;> prefl
    | succ m => cases f; simp [invTerm]; exact Proof.ax .eq_congr_func
  | all h => pintro; exact inv_axiom (k := k + 1) h
  | _ => apply Proof.ax; constructor

theorem inv_proof : hom.onFormula '' Î” âŠ¢ p â†’ Î” âŠ¢ âˆ€' (invFormula 0 p) := by
  intro h
  induction h with
  | hyp h => rcases h with âŸ¨p, h, h'âŸ©; subst h'; rw [invFormula_homFormula]; exact Proof.forall_self.mp (.hyp h)
  | ax h => pintro; exact inv_axiom (k := 0) h
  | mp _ _ ihâ‚ ihâ‚‚ => simp [invFormula_imp (k := 0)] at ihâ‚; exact (Proof.ax .forall_imp).mpâ‚‚ ihâ‚ ihâ‚‚

theorem hom_consistent {Î“ : ğ“›.FormulaSet m} (h : Î“ âŠ¢ âˆƒ' âŠ¤) :
  Consistent Î“ â†’ Consistent (hom.onFormula '' Î“ : (ğ“›.henkinStep n).FormulaSet m) := by
  intro hâ‚ hâ‚‚
  apply inv_proof at hâ‚‚
  simp [invFormula_false (k := 0)] at hâ‚‚
  exact hâ‚ ((Proof.iff_mpr.mp Proof.not_forall_iff).mpâ‚‚ h hâ‚‚)

inductive axioms : (ğ“›.henkinStep n).FormulaSet n where
| henkin (p) : axioms (âˆƒ' (hom.onFormula p) â‡’ (hom.onFormula p)[â†¦â‚› (wit p)]â‚š)

end henkinStep

def FormulaSet.henkinStep (Î“ : ğ“›.FormulaSet n) : (ğ“›.henkinStep n).FormulaSet n :=
  henkinStep.hom.onFormula '' Î“ âˆª henkinStep.axioms

theorem FormulaSet.henkinStep.consistent (h : Î“ âŠ¢ âˆƒ' âŠ¤) :
  Consistent Î“ â†’ Consistent Î“.henkinStep := by
  intro hâ‚ hâ‚‚
  apply Proof.compactness at hâ‚‚
  rcases hâ‚‚ with âŸ¨Î”, hâ‚‚, hâ‚ƒ, hâ‚„âŸ©
  simp [FormulaSet.henkinStep] at hâ‚‚
  apply Set.decompose_subset_union at hâ‚‚
  rcases hâ‚‚ with âŸ¨Î“', A, hâ‚‚, hâ‚…, hâ‚…'âŸ©
  subst hâ‚‚; simp at hâ‚ƒ; rcases hâ‚ƒ with âŸ¨_, hâ‚ƒâŸ©
  revert hâ‚„; apply Set.Finite.induction_on' (C := _) hâ‚ƒ
  Â· intro hâ‚„; simp at hâ‚„
    apply Proof.weaken hâ‚… at hâ‚„
    exact henkinStep.hom_consistent h hâ‚ hâ‚„
  Â· intro a A' hâ‚† hâ‚†' hâ‚†'' hâ‚„ hâ‚„'
    simp at hâ‚„'; apply Proof.deduction.mpr at hâ‚„'
    rcases hâ‚…' hâ‚† with âŸ¨pâŸ©
    apply hâ‚„
    apply (Proof.not_imp_left.mp hâ‚„').mp
    apply Proof.const_generalization (c := henkinStep.wit p)
    Â· intro q h; simp at h; rcases h with h | h
      Â· rcases hâ‚… h with âŸ¨q', _, hâŸ©; subst h; apply henkinStep.wit_not_in_homFormula
      Â· rcases hâ‚…' (hâ‚†' h) with âŸ¨qâŸ©
        simp [Formula.consts, Formula.consts_of_subst]
        constructor
        Â· apply henkinStep.wit_not_in_homFormula
        Â· intro x h
          cases x using Fin.cases with simp [Formula.consts, Term.consts]
          | zero => intro h; apply henkinStep.Func.wit.inj at h; subst h; contradiction
    Â· simp [Formula.consts]; apply henkinStep.wit_not_in_homFormula
    Â· exact Proof.not_imp_right.mp hâ‚„'

def henkinChain (ğ“› : Language) (n : â„•) : â„• â†’ Language
| 0 => ğ“›
| i + 1 => (ğ“›.henkinChain n i).henkinStep n

def henkinize (ğ“› : Language) (n : â„•) : Language := (DirectedSystem.ofChain (ğ“›.henkinChain n) (Î» _ => henkinStep.hom)).directLimit

namespace FormulaSet

def henkinChain (Î“ : ğ“›.FormulaSet n) : (i : â„•) â†’ (ğ“›.henkinChain n i).FormulaSet n
| 0 => Î“
| i + 1 => (Î“.henkinChain i).henkinStep

def henkinize (Î“ : ğ“›.FormulaSet n) : (ğ“›.henkinize n).FormulaSet n :=
  â‹ƒi, (DirectedSystem.homLimit _ i).onFormula '' Î“.henkinChain i

variable {Î“ : ğ“›.FormulaSet n}

theorem henkinize.supset_henkin : Î“.henkinize âŠ† Î” â†’ Henkin Î” := by
  intro hâ‚ p hâ‚‚
  rcases DirectedSystem.formula_of_homLimit p with âŸ¨i, q, hâ‚ƒâŸ©
  exists (DirectedSystem.homLimit _ (i + 1)).onFunc (henkinStep.wit q)
  revert hâ‚‚; apply Proof.mp
  apply Proof.hyp
  apply hâ‚
  rw [â†DirectedSystem.homLimit_comp_hom (h := Nat.le_succ i)] at hâ‚ƒ
  simp [Hom.comp_onFormula, DirectedSystem.ofChain_hom_succ] at hâ‚ƒ
  simp [henkinize]
  exists i + 1, _, .inr (henkinStep.axioms.henkin q)
  simp [hâ‚ƒ, Hom.onFormula, Hom.onTerm, Hom.onFormula_subst_single, Hom.id_onFormula, Vec.eq_nil]
  rfl

variable (hâ‚ : Î“ âŠ¢ âˆƒ' âŠ¤)
include hâ‚

lemma henkinChain.nontrivial : {i : â„•} â†’ Î“.henkinChain i âŠ¢ âˆƒ' âŠ¤
| 0 => hâ‚
| _ + 1 => Proof.weaken (Set.subset_union_left) (henkinStep.hom.on_proof nontrivial)

variable (hâ‚‚ : Consistent Î“)
include hâ‚‚

theorem henkinChain.consistent : {i : â„•} â†’ Consistent (Î“.henkinChain i)
| 0 => hâ‚‚
| _ + 1 => henkinStep.consistent (nontrivial hâ‚) consistent

theorem henkinize.consistent : Consistent Î“.henkinize := by
  intro h
  apply DirectedSystem.proof_of_homLimit at h
  rcases h with âŸ¨i, Î”, p, hâ‚ƒ, hâ‚„, hâ‚…, hâ‚†âŸ©
  cases p <;> simp [Hom.onFormula] at hâ‚„
  rcases DirectedSystem.subset_of_monotone_union
    (DirectedSystem.monotone_chain Î» _ => Set.subset_union_left) hâ‚ƒ hâ‚… with âŸ¨j, hâ‚‡, hâ‚ˆâŸ©
  apply henkinChain.consistent hâ‚ hâ‚‚
  apply Proof.weaken hâ‚ˆ
  exact Hom.on_proof hâ‚†
